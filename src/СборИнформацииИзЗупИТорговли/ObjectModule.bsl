
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс

Процедура СборИнформации() Экспорт

	Информация = Новый ТаблицаЗначений;

	ИнформацияИзТорговли(Информация);
	ИнформацияИзЗуп(Информация);
	ПоказатьИнформацию(Информация);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаЗуп()
	
	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник.Наименование КАК Сотрудник,
	|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо.Наименование КАК ФизическоеЛицо,
	|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо.Код КАК ФизЛицоКод,
	|	КадроваяИсторияСотрудниковСрезПоследних.Должность.Наименование КАК Должность
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудниковСрезПоследних
	|ГДЕ
	|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо.Код В(&КодыФизЛиц)";
	
КонецФункции

Функция ТекстЗапросаТорговли()

	Возврат "ВЫБРАТЬ
			|	Пользователи.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ втУволенные
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|ГДЕ
			|	Пользователи.Ссылка В ИЕРАРХИИ (&Уволенные)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СпрКластеры.Ссылка КАК Кластер,
			|	СпрПодразделения.Ссылка КАК Подразделение,
			|	АктивПользователи.Ссылка КАК Сотрудник,
			|	АктивПользователи.ФизЛицо.ПерсоналКод КАК ФизЛицоКод,
			|	СпрПодразделения.ОтветственноеЛицо
			|ИЗ
			|	Справочник.Кластеры КАК СпрКластеры
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Подразделения КАК СпрПодразделения
			|		ПО СпрКластеры.Ссылка = СпрПодразделения.Кластер
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК АктивПользователи
			|		ПО (СпрПодразделения.Ссылка = АктивПользователи.Подразделение)
			|ГДЕ
			|	НЕ АктивПользователи.Ссылка В
			|		(ВЫБРАТЬ
			|			Уволенные.Ссылка
			|		ИЗ
			|			втУволенные КАК Уволенные)
			|ИТОГИ
			|ПО
			|	Кластер,
			|	Подразделение ТОЛЬКО ИЕРАРХИЯ";

КонецФункции

Функция ТекстЗапросаТорговляЗуп()
	
	Возврат "ВЫБРАТЬ
			|	таб.ФизЛицоКод,
			|	таб.Должность
			|ПОМЕСТИТЬ втКадроваяИстория
			|ИЗ	&КадроваяИстория КАК таб;
			|
			|ВЫБРАТЬ
			|	Таб.Кластер,
			|	Таб.Подразделение,
			|	Таб.Сотрудник,
			|	Таб.ФизЛицоКод,
			|	Таб.ОтветственноеЛицо
			|ПОМЕСТИТЬ втИнформация
			|ИЗ &Информация КАК таб;
			|
			|ВЫБРАТЬ
			|	Информация.Кластер,
			|	Информация.Подразделение,
			|	Информация.Сотрудник,
			|	Информация.ФизЛицоКод,
			|	Информация.ОтветственноеЛицо,
			|	КадроваяИстория.Должность
			|ИЗ
			|	втИнформация КАК Информация
			|	ЛЕВОЕ СОЕДИНЕНИЕ втКадроваяИстория КАК КадроваяИстория
			|	ПО Информация.ФизЛицоКод = КадроваяИстория.ФизЛицоКод
			|УПОРЯДОЧИТЬ ПО
			|   Кластер, Подразделение";
	
КонецФункции

Процедура ИнформацияИзТорговли(Информация)

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаТорговли();
	Запрос.УстановитьПараметр("Уволенные", Справочники.Пользователи.НайтиПоНаименованию("Уволен", Истина));
	Информация = Запрос.Выполнить().Выгрузить();

КонецПроцедуры

Процедура ИнформацияИзЗуп(Информация)
	
	КодыФизЛиц = Информация.ВыгрузитьКолонку("ФизЛицоКод");
	
	ТекстЗапроса = ТекстЗапросаЗуп();
	ПараметрыЗапроса = Новый Структура("КодыФизЛиц", КодыФизЛиц);
	КадроваяИстория = ОбщиеФункции.ВыполнитьЗапросXDTO(ТекстЗапроса, ПараметрыЗапроса, Справочники.СтрокиСоединенияСБазами.ВебСервисЗУП_3_1);
	ДобавитьКадровуюИнформацию(Информация, КадроваяИстория);	
	
КонецПроцедуры

Функция ДобавитьКадровуюИнформацию(Информация, КадроваяИстория)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаТорговляЗуп();
	Запрос.УстановитьПараметр("Информация", Информация);
	Запрос.УстановитьПараметр("КадроваяИстория", КадроваяИстория);
	Информация = Запрос.Выполнить().Выгрузить();	
	
КонецФункции

Процедура ПоказатьИнформацию(Информация)
	
	Построитель = Новый ПостроительОтчета;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(Информация);
	
	ТабДокумент = Новый ТабличныйДокумент;
	Построитель.Вывести(ТабДокумент);
	ТабДокумент.Показать("Информация по кластерам, подразделениям, сотрудникам");
	ТабДокумент.Записать(ПолноеИмя, ТипФайлаТабличногоДокумента.XLSX);
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
