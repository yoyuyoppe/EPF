#Область ПЕРЕМЕННЫЕ

Перем Соединение;
Перем СвойстваПартнера Экспорт;
Перем ТаблицаТоваровПоклажедателя;
Перем ТаблицаЕдиницИзмеренийПоклажедателя;
Перем ТаблицаТоваровДистрибьютора;
Перем ТаблицаОсновныхКарточекНоменклатуры;
Перем ТаблицаНовыхЗаявокНаОбновлениеСтатусовПоставки;
Перем ТаблицаСкладовПоклажедателей;
Перем МетодыОбменаСПартнером;

#КонецОбласти 

#Область ПрограммныйИнтерфейс
// Печатные формы
Процедура ПолучитьПечатныеФормы() Экспорт
	
	Перем Запрос;
	
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_ПолучитьПечФормы;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	ТаблицаЗаявок = ПолучитьЗаявкиНаЗагрузкуПечатныхФорм();
	
	ТаблицаПечФорм = СформироватьТаблицуПечФорм();
	
	Для каждого СтрТаб Из ТаблицаЗаявок Цикл
		
		Если НЕ ЗначениеЗаполнено(СокрЛП(СтрТаб.НомерПоставки)) Тогда
			Продолжить;
		КонецЕсли; 
		
		ПолучитьТокенПечати();
		
		Попытка
			
			СоздатьЗапросДляМетода(СтруктураМетода, Запрос, Новый Структура("Plant, shipmentNumber", СвойстваПартнера.КодДистрибьютора, СокрЛП(СтрТаб.НомерПоставки)));
			
			Ответ = Соединение.Получить(Запрос);
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если Ответ.КодСостояния >= 200 И Ответ.КодСостояния < 300 Тогда
				
				ДобавитьСтрокиВТаблицуПечФорм(ТаблицаПечФорм, Данные);
				ЗагрузитьПечатныеФормы(ТаблицаПечФорм);
				
				Если НЕ ЗначениеЗаполнено(ТаблицаПечФорм) Тогда
					Сообщить(ОбщиеФункции._СтрШаблон_("Печатные формы по транспортировке №%1 не найдены", СтрТаб.НомерПоставки), СтатусСообщения.Внимание);
					Продолжить;
				КонецЕсли; 
				
				Если ТаблицаПечФорм.Количество() > 2 Тогда
					_3PLСервер.ОперацияВыполненаУспешно(СтрТаб.Объект, Строка(Партнер), "1С", Ответ.ПолучитьТелоКакСтроку());
				КонецЕсли; 
				
				ТаблицаПечФорм.Очистить();
				
			ИначеЕсли Ответ.КодСостояния = 401 тогда
				
				Если ТипЗнч(Данные) = Тип("Структура")
					И Данные.Свойство("detail")
					И Данные.detail = "Full authentication is required to access this resource" Тогда 
					// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
					СвойстваПартнера.Токен = "";
					
					ОбновитьТокенДоступа();
					
					ПолучитьПечатныеФормы();
					
				КонецЕсли;
				
			Иначе
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
		Исключение
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры
// Обновление статусов транспортировок
Процедура ОбновитьСтатусыТранспортировок() Экспорт
	
	Перем Запрос;
	
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_ОбновитьПоставки;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
	
	ТаблицаЗаявок = ПолучитьЗаявкиНаОбновлениеПоставок();
	
	Попытка
		
		Для каждого СтрТаб Из ТаблицаЗаявок Цикл
			
			ТелоЗапроса = СформироватьТелоЗапросаДляОбновленияПоставки(СтрТаб.Объект, СокрЛП(СтрТаб.НомерТранспортировки), СтрТаб.Идентификатор);
			
			Запрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.Системная);
			
			Ответ = Соединение.ОтправитьДляОбработки(Запрос);
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если Ответ.КодСостояния > 200 И Ответ.КодСостояния < 300 Тогда
				
				Если СтрТаб.Объект = NULL Тогда
					СтрТаб.Объект = Документы.РегистраторЗаписи.СоздатьНовыйДокумент();
				КонецЕсли; 
				
				_3PLСервер.ОперацияВыполненаУспешно(СтрТаб.Объект, "1С", Строка(Партнер), ТелоЗапроса, СтрТаб.Идентификатор);
				СоздатьЗаявкуНаПолучениеПечатныхФорм(СтрТаб.Объект, СокрЛП(СтрТаб.НомерТранспортировки));
				
				ОбщегоНазначения.п_Задержка(2);
				
			ИначеЕсли Ответ.КодСостояния = 401
				И ТипЗнч(Данные) = Тип("Структура")
				И Данные.Свойство("detail")
				И Данные.detail = "Full authentication is required to access this resource" Тогда 
				// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
				СвойстваПартнера.Токен = "";
				
				ОбновитьТокенДоступа();
				
				ОбновитьСтатусыТранспортировок();
				
			Иначе
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
		КонецЦикла; 
		
	Исключение
		ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры
// Отправка коррректировок остатков 
Процедура ОтправитьКорректировкиОстатков() Экспорт
	
	Перем Запрос;
	
	//Сообщить("Операция временно недоступна. Обратитесь в ИТ отдел");
	//Возврат;
	
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_КорректировкаОстатков;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
	
	ВыборкаДанных = ПолучитьДанныеПоДвижениямКорректировкиОстатков();
	
	Пока ВыборкаДанных.Следующий() Цикл
		
		Попытка
			
			ТелоЗапроса = СформироватьТелоЗапросаПоРасхождениямПриПересчете(ВыборкаДанных);
			
			Запрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.Системная);
			
			Ответ = Соединение.ОтправитьДляОбработки(Запрос);
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если Ответ.КодСостояния > 200 И Ответ.КодСостояния < 300 Тогда
				_3PLСервер.ДобавитьЗаписьПоДокументуВЖурналОпераций(ВыборкаДанных.Ссылка, "1С", Строка(Партнер), Справочники.СтатусыЗаявок.УспешноОбработана,
					"inventorymovement_"+Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMddHHmmss"), ТекущаяДатаСеанса(), ТекущаяДатаСеанса(), ТелоЗапроса);
			ИначеЕсли Ответ.КодСостояния = 401
				И ТипЗнч(Данные) = Тип("Структура")
				И Данные.Свойство("detail")
				И Данные.detail = "Full authentication is required to access this resource" Тогда 
				// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
				СвойстваПартнера.Токен = "";
				
				ОбновитьТокенДоступа();
				
				ОтправитьКорректировкиОстатков();
				
			Иначе
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
		Исключение
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		КонецПопытки
		
	КонецЦикла;
	
КонецПроцедуры
// Отправка текущих остатков
Процедура ОтправитьТекущиеОстатки() Экспорт
	
	Перем Запрос;
	
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_ТекущиеОстатки;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
	
	Попытка
		
		СформироватьТаблицуСкладовПоклажедателей();
		
		ТелоЗапроса = СформироватьТелоЗапросаПоТекущимОстаткам();
		
		Запрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.Системная);
		
		Ответ = Соединение.ОтправитьДляОбработки(Запрос);
		
		Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
		
		Если Ответ.КодСостояния > 200 И Ответ.КодСостояния < 300 Тогда
			_3PLСервер.ОперацияВыполненаУспешно("inventorysnapshot", "1С", Строка(Партнер), ТелоЗапроса);
		ИначеЕсли Ответ.КодСостояния = 401
			И ТипЗнч(Данные) = Тип("Структура")
			И Данные.Свойство("detail")
			И Данные.detail = "Full authentication is required to access this resource" Тогда 
			// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
			СвойстваПартнера.Токен = "";
			
			ОбновитьТокенДоступа();
			
			ОтправитьТекущиеОстатки();
			
		Иначе
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
		КонецЕсли;
		
	Исключение
		ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры
// Загрузка документов из SAP
Процедура ПолучитьДокументы() Экспорт
	
	Перем Запрос, ДатаПоследнегоОбменаПоДокументам;
	
	//Тест_ПолучитьДокументы();
	//Возврат;
		
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	Если СвойстваПартнера.Свойство("ДатаПоследнегоОбменаПоДокументам", ДатаПоследнегоОбменаПоДокументам) Тогда
		СвойстваПартнера.ДатаПоследнегоОбмена = ДатаПоследнегоОбменаПоДокументам;
	КонецЕсли;
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_Документы;
	
	СформироватьТаблицаТоваровПоклажедателя();
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	НомерСтраницы = СтруктураМетода.Параметры.Получить("page");
	
	КоличествоОбъектовНаСтранице = СтруктураМетода.Параметры.Получить("size");
	
	Если КоличествоОбъектовНаСтранице = Неопределено Тогда
		КоличествоОбъектовНаСтранице = 50;
	КонецЕсли; 
	
	Пока Истина Цикл
		
		Попытка
			
			СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
			
			Ответ = Соединение.Получить(Запрос);
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если Ответ.КодСостояния = 200 Тогда
				СоздатьДокументы(ОпределитьТребуемыеДействияПоРаботеСДокументами(ПреобразоватьДанныеПоДокументамВТаблицуЗначений(Данные)));
			ИначеЕсли Ответ.КодСостояния = 401
				И ТипЗнч(Данные) = Тип("Структура")
				И Данные.Свойство("detail")
				И Данные.detail = "Full authentication is required to access this resource" Тогда 
				// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
				СвойстваПартнера.Токен = "";
				
				ОбновитьТокенДоступа();
				
				ПолучитьДокументы();
				
				// Заглушка, чтобы завершить успешно предшествующие методы в рекурсии
				Данные = Новый Массив;
				
			Иначе
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
		Исключение
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		КонецПопытки;
		
		Если Данные.Количество() < КоличествоОбъектовНаСтранице Тогда
			Прервать;
		КонецЕсли;
		// Изменим нумерацию страницы для получения данных
		Если НомерСтраницы <> Неопределено Тогда
			НомерСтраницы = НомерСтраницы + 1;
			СтруктураМетода.Параметры.Вставить("page", НомерСтраницы);
		КонецЕсли;
		
	КонецЦикла; 
	
	СтруктураЗаписи = Новый Структура("Объект, Свойство, Значение", Партнер, "ДатаПоследнегоОбменаПоДокументам", ТекущаяДатаСеанса());
	
	РегистрыСведений.ДополнительныеРеквизиты_3PL.ДобавитьЗапись(СтруктураЗаписи);
	
КонецПроцедуры
// Получение номеров транспортировок
Процедура ПолучитьТранспортировки() Экспорт
	
	Перем Запрос;
	
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_Транспортировки;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	НомерСтраницы = СтруктураМетода.Параметры.Получить("page");
	
	КоличествоОбъектовНаСтранице = СтруктураМетода.Параметры.Получить("size");
	
	Если КоличествоОбъектовНаСтранице = Неопределено Тогда
		КоличествоОбъектовНаСтранице = 50;
	КонецЕсли; 
	
	Пока Истина Цикл
		
		Попытка
			
			СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
			
			Ответ = Соединение.Получить(Запрос);
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если Ответ.КодСостояния = 200 Тогда
				ЗаполнитьНомераТранспортировок(ОпределитьДокументыДляТранспортировки(ПреобразоватьДанныеПоТранспортировкамВТаблицуЗначений(Данные)));
			ИначеЕсли Ответ.КодСостояния = 401
				И ТипЗнч(Данные) = Тип("Структура")
				И Данные.Свойство("detail")
				И Данные.detail = "Full authentication is required to access this resource" Тогда 
				// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
				СвойстваПартнера.Токен = "";
				
				ОбновитьТокенДоступа();
				
				ПолучитьТранспортировки();
				// Заглушка, чтобы завершить успешно предшествующие методы в рекурсии
				Данные = Новый Массив;
				
			Иначе
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
		Исключение
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		КонецПопытки;
		
		Если Данные.Количество() < КоличествоОбъектовНаСтранице Тогда
			Прервать;
		КонецЕсли;
		// Изменим нумерацию страницы для получения данных
		Если НомерСтраницы <> Неопределено Тогда
			НомерСтраницы = НомерСтраницы + 1;
			СтруктураМетода.Параметры.Вставить("page", НомерСтраницы);
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНомераТранспортировок(ТаблицаДанных)
	
	Если ТаблицаДанных.Количество() = 0 Тогда
		Сообщить(ОбщиеФункции._СтрШаблон_("На дату '%1' нет данных по транспортировкам", СвойстваПартнера.ДатаПоследнегоОбмена), СтатусСообщения.Информация);
		Возврат;
	КонецЕсли; 
	
	МассивМаршрутовСТранспортировкой = Новый Массив;
	
	Для каждого СтрТаб Из ТаблицаДанных Цикл
		
		Если СтрТаб.ДокументСсылка = Неопределено Тогда
			
			Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось заполнить номер транспортировки '%1' по причине: не найден документ отгрузки по номеру %2",
				СтрТаб.НомерТранспортировки, СтрТаб.НомерВходящегоДокумента));
				
			Продолжить;
			
		КонецЕсли;
			
		ОбщиеФункции.УстановитьЗначениеСвойства(Новый Структура("Объект, Свойство, Значение",
			СтрТаб.ДокументСсылка, СвойстваПартнера.Свойство_НомерТранспортировки, СтрТаб.НомерТранспортировки));
			
		Если МассивМаршрутовСТранспортировкой.Найти(СтрТаб.МаршрутЭкспедитора) = Неопределено Тогда
				
			Если НЕ ЗначениеЗаполнено(СтрТаб.МаршрутЭкспедитора) Тогда
				Продолжить;
			КонецЕсли; 	
				
			ОбщиеФункции.УстановитьЗначениеСвойства(Новый Структура("Объект, Свойство, Значение",
				СтрТаб.МаршрутЭкспедитора, СвойстваПартнера.Свойство_НомерТранспортировки, СтрТаб.НомерТранспортировки));
				
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура СоздатьДокументы(ТаблицаДанных)
	
	Для каждого СтрТаб Из ТаблицаДанных Цикл
		
		Если СтрТаб.ТребуемоеДействие = "Добавление" Тогда
			
			ДокументСсылка = Неопределено;
			
			Если СтрТаб.ТипДокумента = "7" Тогда
				
				Попытка
					_3PLСервер.СоздатьЗаказПоставщику(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки), Строка(Партнер), ДокументСсылка);
				Исключение
					Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать заказ поставщику №'%1' по причине: 
					|%2", СтрТаб.НомерВходящегоДокумента, ОписаниеОшибки()));
				КонецПопытки;
				
				Если ЗначениеЗаполнено(ДокументСсылка) И ЗначениеЗаполнено(СтрТаб.НомерТранспортировки) Тогда
					ОбщиеФункции.УстановитьЗначениеСвойства(Новый Структура("Объект, Свойство, Значение",
						ДокументСсылка, СвойстваПартнера.Свойство_НомерТранспортировки, СтрТаб.НомерТранспортировки));
				КонецЕсли; 
					
			КонецЕсли;
			
			Если СтрТаб.ТипДокумента = "J" Тогда
				
				Попытка
					_3PLСервер.СоздатьДокументыОтгрузки(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки), Строка(Партнер), ДокументСсылка);
					Сообщить(ОбщиеФункции._СтрШаблон_("Загружен выкуп товара '%1'", ДокументСсылка), СтатусСообщения.Информация);
				Исключение
					Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать документы отгрузки по заказу №'%1' по причине: 
					|%2", СтрТаб.НомерВходящегоДокумента, ОписаниеОшибки()));
				КонецПопытки;
				
				Если СтрТаб.Контрагент.ИНН = "5504086398" Тогда // ООО "ТД "Шкуренко"
					
					ДокументСсылкаЗаказПоставщику = Неопределено;
					
					КонвертироватьТоварыВыкупаВТоварыДистрибьютора(СтрТаб.Товары);
					
					СтрТаб.ДоговорКонтрагента = СвойстваПартнера.ОсновнойДоговорПриемаНаХранение;
					СтрТаб.Контрагент         = СвойстваПартнера.ОсновнойПоставщик;
					
					Попытка
						_3PLСервер.СоздатьЗаказПоставщику(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки), Строка(Партнер), ДокументСсылкаЗаказПоставщику);
					Исключение
						Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать заказ поставщику на основании выкупа №'%1' по причине: 
						|%2", СтрТаб.НомерВходящегоДокумента, ОписаниеОшибки()));
					КонецПопытки;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если СтрТаб.ТипДокумента = "T" И СтрТаб.ВидОперации <> "ZLOR" Тогда
				Попытка
					_3PLСервер.СоздатьВозвратОтПокупателя(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки), Строка(Партнер), ДокументСсылка);
				Исключение
					Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать возврат от покупателя №'%1' по причине: 
					|%2", СтрТаб.НомерВходящегоДокумента, ОписаниеОшибки()));
				КонецПопытки;
			КонецЕсли;
			
			Если СтрТаб.ТипДокумента = "T" И СтрТаб.ВидОперации = "ZLOR" Тогда
				Попытка
					СоздатьИнвентаризациюДоставкиСводную(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки));
				Исключение
					Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать сводную инвентаризацию доставки №'%1' по причине: 
					|%2", СтрТаб.НомерВходящегоДокумента, ОписаниеОшибки()));
				КонецПопытки;
			КонецЕсли;
			
			Если ДокументСсылка <> Неопределено Тогда
				
				ОбщиеФункции.УстановитьЗначениеСвойства(Новый Структура("Объект, Свойство, Значение",
					ДокументСсылка, СвойстваПартнера.Свойство_НомерВходящегоДокумента, СтрТаб.НомерВходящегоДокумента));
					
				ОбщиеФункции.УстановитьЗначениеСвойства(Новый Структура("Объект, Свойство, Значение",
					ДокументСсылка, СвойстваПартнера.Свойство_ДатаВходящегоДокумента, СтрТаб.ДатаВходящегоДокумента));
					
			КонецЕсли; 
			
		КонецЕсли;
		
		Если СтрТаб.ТребуемоеДействие = "Обновление" Тогда
			
			Если СтрТаб.ТипДокумента = "7" Тогда
				Попытка
					_3PLСервер.ОбновитьЗаказПоставщику(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки), СтрТаб.ДокументСсылка);
				Исключение
					Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось обновить заказ поставщику №'%1' по причине: 
					|%2", СтрТаб.НомерВходящегоДокумента, ОписаниеОшибки()));
				КонецПопытки;
			КонецЕсли;
			
			Если СтрТаб.ТипДокумента = "J" Тогда
				Попытка
					_3PLСервер.ОбновитьДокументыОтгрузки(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки), СтрТаб.ДокументСсылка);
				Исключение
					Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось обновить документ №'%1' по причине: 
					|%2", СтрТаб.НомерВходящегоДокумента, ОписаниеОшибки()));
				КонецПопытки;
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры
// Загрузка номенклатуры из SAP
Процедура ПолучитьНоменклатуру() Экспорт
	
	Перем Запрос, ДатаПоследнегоОбменаПоМатериалам;
	
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	Если СвойстваПартнера.Свойство("ДатаПоследнегоОбменаПоМатериалам", ДатаПоследнегоОбменаПоМатериалам) Тогда
		СвойстваПартнера.ДатаПоследнегоОбмена = ДатаПоследнегоОбменаПоМатериалам;
	КонецЕсли; 
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_Материалы;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	НомерСтраницы = СтруктураМетода.Параметры.Получить("page");
	
	КоличествоОбъектовНаСтранице = СтруктураМетода.Параметры.Получить("size");
	
	Если КоличествоОбъектовНаСтранице = Неопределено Тогда
		КоличествоОбъектовНаСтранице = 50;
	КонецЕсли; 
	
	Пока Истина Цикл
		
		Попытка
			
			СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
			
			Ответ = Соединение.Получить(Запрос);
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если Ответ.КодСостояния = 200 Тогда
				ОбновитьНоменклатуру(ОпределитьТребуемыеДействияПоРаботеСНоменклатурой(ПреобразоватьДанныеПоТоварамВТаблицуЗначений(Данные)));
			ИначеЕсли Ответ.КодСостояния = 401
				И ТипЗнч(Данные) = Тип("Структура")
				И Данные.Свойство("detail")
				И Данные.detail = "Full authentication is required to access this resource" Тогда 
				// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
				СвойстваПартнера.Токен = "";
				
				ОбновитьТокенДоступа();
				
				ПолучитьНоменклатуру();
				// Заглушка, чтобы завершить успешно предшествующие методы в рекурсии
				Данные = Новый Массив;
				
			Иначе
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
		Исключение
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		КонецПопытки;
		
		Если Данные.Количество() < КоличествоОбъектовНаСтранице Тогда
			Прервать;
		КонецЕсли;
		// Изменим нумерацию страницы для получения данных
		Если НомерСтраницы <> Неопределено Тогда
			НомерСтраницы = НомерСтраницы + 1;
			СтруктураМетода.Параметры.Вставить("page", НомерСтраницы);
		КонецЕсли; 
		
	КонецЦикла;
	
	СтруктураЗаписи = Новый Структура("Объект, Свойство, Значение", Партнер, "ДатаПоследнегоОбменаПоМатериалам", ТекущаяДатаСеанса());
	
	РегистрыСведений.ДополнительныеРеквизиты_3PL.ДобавитьЗапись(СтруктураЗаписи);
	
КонецПроцедуры

Процедура ОбновитьНоменклатуру(ТаблицаДанных)
	
	СформироватьТаблицаТоваровДистрибьютора();
	
	РазрешитьСозданиеДублей = (ЗначениеЗаполнено(ТаблицаТоваровДистрибьютора) 
		И СвойстваПартнера.Свойство("ГруппаНоменклатурыДублей") И ЗначениеЗаполнено(СвойстваПартнера.ГруппаНоменклатурыДублей));
	
	Для каждого СтрТаб Из ТаблицаДанных Цикл
		
		Если СтрТаб.ТребуемоеДействие = "Добавление" Тогда
			
			Попытка
				_3PLСервер.СоздатьНоменклатуру(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки));
			Исключение
				Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать номенклатуру '%1' по причине:
				|%2", СтрТаб.Наименование, ОписаниеОшибки()));
			КонецПопытки; 
			
		КонецЕсли;
		
		Если РазрешитьСозданиеДублей 
			И ТаблицаТоваровДистрибьютора.Найти(СтрТаб.Артикул, "Артикул") = Неопределено Тогда
			
			СтруктураКлона = ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки);
			ЗаполнитьЗначенияСвойств(СтруктураКлона, СтрТаб);
			
			СтруктураКлона.Родитель = СвойстваПартнера.ГруппаНоменклатурыДублей;
			
			Попытка
				_3PLСервер.СоздатьНоменклатуру(СтруктураКлона);
			Исключение
				Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать номенклатуру клон '%1' по причине:
				|%2", СтрТаб.Наименование, ОписаниеОшибки()));
			КонецПопытки;
			
			Ссылка = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", СтрТаб.Артикул, СвойстваПартнера.ГруппаНоменклатурыДублей);
			
			Если НЕ Ссылка.Пустая() Тогда
				
				Попытка
					ТоварОбъект = Ссылка.ПолучитьОбъект();
					ТоварОбъект.Наименование = Ссылка.НаименованиеПолное;
					ТоварОбъект.Комментарий = "Создана на основании товара 3PL с артикулом " + СтруктураКлона.Артикул;
					ТоварОбъект.Записать();
					Сообщить(ОбщиеФункции._СтрШаблон_("Создана новая номенклатура '%1' с артикулом '%2'", Ссылка, СтрТаб.Артикул), СтатусСообщения.Информация);
				Исключение
				    Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать номенклатуру клон '%1' с артикулом '%2' по причине: %3", Ссылка, СтрТаб.Артикул,  ОписаниеОшибки()), СтатусСообщения.Информация);
				КонецПопытки;
				
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЦикла;  
	
КонецПроцедуры
// Загрузка партий из SAP
Процедура ПолучитьПартии() Экспорт
	// ВНИМАНИЕ. Под партией подразумивается серия номенклатуры
	Перем Запрос, ДатаПоследнегоОбменаПоПартиям;
	
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	Если СвойстваПартнера.Свойство("ДатаПоследнегоОбменаПоПартиям", ДатаПоследнегоОбменаПоПартиям) Тогда
		СвойстваПартнера.ДатаПоследнегоОбмена = ДатаПоследнегоОбменаПоПартиям;
	КонецЕсли; 
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_Партии;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	Попытка
		
		СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
		
		Ответ = Соединение.Получить(Запрос);
		
		Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
		
		Если Ответ.КодСостояния = 200 Тогда
			ОбновитьПартии(ОпределитьТребуемыеДействияПоРаботеСПартиями(ПреобразоватьДанныеПоПартиямВТаблицуЗначений(Данные)));
		ИначеЕсли Ответ.КодСостояния = 401
			И ТипЗнч(Данные) = Тип("Структура")
			И Данные.Свойство("detail")
			И Данные.detail = "Full authentication is required to access this resource" Тогда 
			// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
			СвойстваПартнера.Токен = "";
			
			ОбновитьТокенДоступа();
			
			ПолучитьПартии();
			
		Иначе
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
		КонецЕсли;
		
	Исключение
		ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
	КонецПопытки;
	
	СтруктураЗаписи = Новый Структура("Объект, Свойство, Значение", Партнер, "ДатаПоследнегоОбменаПоПартиям", ТекущаяДатаСеанса());
	
	РегистрыСведений.ДополнительныеРеквизиты_3PL.ДобавитьЗапись(СтруктураЗаписи);
	
КонецПроцедуры

Процедура ОбновитьПартии(ТаблицаДанных) 
	
	Для каждого СтрТаб Из ТаблицаДанных Цикл
		
		Если СтрТаб.ТребуемоеДействие = "Добавление" Тогда
			Попытка
				_3PLСервер.СоздатьСериюНоменклатуры(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки));
			Исключение
				Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось создать серию по товару '%1' (Номер партии: %2) по причине:
				|%3", ?(ЗначениеЗаполнено(СтрТаб.Владелец), СтрТаб.Владелец, СтрТаб.КодПартнера), СтрТаб.НомерПартииПоставщика, ОписаниеОшибки()));
			КонецПопытки;
		КонецЕсли;
		
		Если СтрТаб.ТребуемоеДействие = "Обновление" Тогда
			Попытка
				_3PLСервер.ОбновитьСериюНоменклатуры(ПреобразоватьСтрокуТабЗначВСтруктуру(СтрТаб, ТаблицаДанных.Колонки), СтрТаб.СерияНоменклатуры);
			Исключение
				Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось обновить серию по товару '%1' (Номер партии: %2) по причине:
				|%3", ?(ЗначениеЗаполнено(СтрТаб.Владелец), СтрТаб.Владелец, СтрТаб.КодПартнера), СтрТаб.НомерПартииПоставщика, ОписаниеОшибки()));
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры
// Отправка пикингов (подтверждений по поставкам)
Процедура ПодтвердитьПоставки() Экспорт
	
	Перем Запрос;

	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	ЗаполнитьТаблицуНовыхЗаявокНаОбновлениеСтатусовПоставки();
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_ПодтвердитьПоставки;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	ТаблицаДокументов = _3PLСервер.ПолучитьНеподтвержденныеПоставки(Строка(Партнер));
	
	СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
	
	МассивТранспортировок = Новый Массив;
	
	Для каждого СтрТаб Из ТаблицаДокументов Цикл
		
		Если СтрТаб.ДокументПодтверждения = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		Попытка
			
			ТелоЗапроса = СформироватьТелоЗапросаДляПодтвержденияПоставки(СтрТаб.Объект, СтрТаб.ДокументПодтверждения, СтрТаб.Идентификатор);
			
			Запрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.Системная);
			
			Ответ = Соединение.ОтправитьДляОбработки(Запрос);
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если Ответ.КодСостояния > 200 И Ответ.КодСостояния < 300 Тогда
				_3PLСервер.ОперацияВыполненаУспешно(СтрТаб.Объект, Строка(Партнер), "1С", ТелоЗапроса);
				Если ТипЗнч(СтрТаб.Объект) = Тип("ДокументСсылка.ОтгрузкаТоваровУслуг") Тогда
					СоздатьЗаявкуНаОперациюОбновленияПоставок(СтрТаб.Объект, МассивТранспортировок);
				КонецЕсли; 
			ИначеЕсли Ответ.КодСостояния = 401
				И ТипЗнч(Данные) = Тип("Структура")
				И Данные.Свойство("detail")
				И Данные.detail = "Full authentication is required to access this resource" Тогда 
				// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
				СвойстваПартнера.Токен = "";
				
				ОбновитьТокенДоступа();
				
				ПодтвердитьПоставки();
				
				Прервать;
				
			Иначе
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
		Исключение
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры
// Отправка маршрутов в SAP 
Процедура ВыгрузитьМаршрутыЭкспедиторов() Экспорт
	
	Перем Запрос;

	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_ВыгрузитьМаршруты;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	ТаблицаДокументов = _3PLСервер.ПолучитьМаршрутыЭкспедиторовДляОтправки(Строка(Партнер));
	
	СоздатьЗапросДляМетода(СтруктураМетода, Запрос);
	
	Для каждого СтрТаб Из ТаблицаДокументов Цикл
		
		Попытка
			
			МассивРасхождений = Новый Массив;
			
			ТелоЗапроса = СформироватьТелоЗапросаДляОтправкиМаршрута(СтрТаб.МаршрутЭкспедитора);
			
			Если ТелоЗапроса = "" Тогда
				Продолжить;
			КонецЕсли; 
			
			Запрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.UTF8);
			
			Ответ = Соединение.ОтправитьДляОбработки(Запрос);
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если Ответ.КодСостояния > 200 И Ответ.КодСостояния < 300 Тогда
				_3PLСервер.ОперацияВыполненаУспешно(СтрТаб.МаршрутЭкспедитора, "1С", Строка(Партнер), ТелоЗапроса);
			ИначеЕсли Ответ.КодСостояния = 401
				И ТипЗнч(Данные) = Тип("Структура")
				И Данные.Свойство("detail")
				И Данные.detail = "Full authentication is required to access this resource" Тогда 
				// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
				СвойстваПартнера.Токен = "";
				
				ОбновитьТокенДоступа();
				
				ВыгрузитьМаршрутыЭкспедиторов();
				
				Прервать;
				
			Иначе
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
		Исключение
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		КонецПопытки;
			
	КонецЦикла;
	
КонецПроцедуры
 
Процедура ОбновитьТокенДоступа() Экспорт
	
	Если СвойстваПартнера = Неопределено Тогда
		ПрочитатьСвойстваПартнера();
	КонецЕсли;
	
	Если СвойстваПартнера.Свойство("Токен")
		И ЗначениеЗаполнено(СвойстваПартнера.Токен) Тогда
		Возврат;
	КонецЕсли; 
	
	Защита = Новый ЗащищенноеСоединениеOpenSSL(Новый СертификатКлиентаWindows, Новый СертификатыУдостоверяющихЦентровWindows);

	Попытка
		СоединениеДляАвторизации = Новый HTTPСоединение(СвойстваПартнера.АдресСервисаАвторизации,, СвойстваПартнера.Пользователь, СвойстваПартнера.Пароль,,300, Защита);
	Исключение
		ВызватьИсключение ОбщиеФункции._СтрШаблон_("Не удалось подключиться к сервису '%1' по причине: %2", СвойстваПартнера.АдресСервисаАвторизации, ОписаниеОшибки());
	КонецПопытки;
	
	ТелоЗапроса =  СокрЛП("grant_type=client_credentials&scope="+СвойстваПартнера.ОбластьВидимостиДистрибьютораВИнтерфейсе);
	
	Запрос = Новый HTTPЗапрос;
	Запрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.Системная);
	Запрос.АдресРесурса = СвойстваПартнера.АдресРесурсаАвторизации;
	Запрос.Заголовки.Вставить("Accept", "*/*");
	Запрос.Заголовки.Вставить("Connection", "keep-alive");
	Запрос.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	Запрос.Заголовки.Вставить("Host", СвойстваПартнера.АдресСервисаАвторизации);
	Запрос.Заголовки.Вставить("Content-Length", СтрДлина(ТелоЗапроса));
	
	Ответ = СоединениеДляАвторизации.ОтправитьДляОбработки(Запрос);
	
	Если Ответ.КодСостояния = 200 Тогда
		
		Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
		
		СтруктураЗаписи = РегистрыСведений.ДополнительныеРеквизиты_3PL.ПолучитьСтруктуруЗаписи();
		СтруктураЗаписи.Объект = Партнер;
		СтруктураЗаписи.Свойство = "Токен";
		СтруктураЗаписи.ЗначениеСтрока = ОбщиеФункции._СтрШаблон_("%1 %2", Данные.token_type, Данные.access_token);
		
		РегистрыСведений.ДополнительныеРеквизиты_3PL.ДобавитьЗапись(СтруктураЗаписи);
		
		СвойстваПартнера.Вставить(СтруктураЗаписи.Свойство, СтруктураЗаписи.ЗначениеСтрока);
		
	Иначе
		
		ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		
	КонецЕсли;
	
	Соединение = Неопределено;
	
КонецПроцедуры

Процедура ПолучитьТокенПечати()
	
	Перем Запрос;
	
	Если Соединение = Неопределено Тогда
		УстановитьСоединение();
	КонецЕсли;
	
	ОписаниеМетода = МетодыОбменаСПартнером.МетодОбмена_ПолучитьТокенПечати;
	
	СтруктураМетода = СформироватьСтруктуруМетода(ОписаниеМетода);
	
	СоздатьЗапросДляМетода(СтруктураМетода, Запрос, Новый Структура());
	
	Попытка
		
		Ответ = Соединение.Получить(Запрос);
		
		Если Ответ.КодСостояния >= 200 И Ответ.КодСостояния < 300 Тогда
			СвойстваПартнера.ТокенПечати = Ответ.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
		ИначеЕсли Ответ.КодСостояния = 401 тогда
			
			Данные = ПолучитьДанныеИзОтвета(Ответ, Запрос.АдресРесурса);
			
			Если ТипЗнч(Данные) = Тип("Структура")
				И Данные.Свойство("detail")
				И Данные.detail = "Full authentication is required to access this resource" Тогда 
				// Время действия токена истекло. Обновим его и ещё раз вызовем метод обмена
				СвойстваПартнера.Токен = "";
				
				ОбновитьТокенДоступа();
				
				ПолучитьТокенПечати();
				
			КонецЕсли;
			
		Иначе
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, Ответ.ПолучитьТелоКакСтроку());
		КонецЕсли;
		
	Исключение
		ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры
 
#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПроверкаЗаполнения()
	
	ТекстСообщения = "";
	
	Если Партнер.Пустая() Тогда
		ТекстСообщения = ТекстСообщения + "Не удалось получить настройки партнера по причине: не заполнен 'Партнер'";
	КонецЕсли;
	
	Если НЕ Партнер.Пустая() И СвойстваПартнера = Неопределено Тогда
		ТекстСообщения = ТекстСообщения + ОбщиеФункции._СтрШаблон_("Не найдены свойства партнера ", Партнер);
	КонецЕсли;
	
	МассивПроверяемыхСвойств = ОбщиеФункции.РазложитьСтрокуВМассив("АдресСервиса;АдресСервисаАвторизации;АдресРесурсаАвторизации;КодДистрибьютора;ОбластьВидимостиДистрибьютораВИнтерфейсе;Пароль;Пользователь;ИспользоватьЗащищенноеСоединение
	|Контракт;Склад;ГруппаНоменклатуры;ОсновнойПоставщик;ОсновнаяОрганизация;ОсновнойДоговорПриемаНаХранение;
	|ОсновнойТипЦенКонтрагента;КачествоДляБлокировки;ОсновноеПодразделение;ОсновнойСпособРасчетов;ОсновнойВидПоставки;ВидПоставки_Самовывоз;
	|НаименованиеДоговораОтветХранения;Свойство_КодГрузополучателя;Свойство_НомерВходящегоДокумента;Свойство_НомерТранспортировки;Свойство_КодПричиныНеполнойКомплектацииПепси;
	|ОсновнойКодПричиныНеполнойКомплектации;ОсновнойСклад;КачествоПретензия;КостЦентр;СобственныеАрендодатели;Свойство_КодСотрудникаСАП;Свойство_КодТранспортаСАП;
	|ТехническоеИмяМестаОтгрузки;КаталогХраненияПечатныхФорм;НомерВиртуальнойПартии;ГруппаХранения;ВремяНачалаМаршрута;ВремяОкончанияМаршрута;СреднееВремяПосещения", ";");
	
	Для каждого ИмяСвойства Из МассивПроверяемыхСвойств Цикл
		
		Если ПустаяСтрока(ИмяСвойства) Тогда
			Продолжить;
		КонецЕсли; 
		
		Если НЕ СвойстваПартнера.Свойство(ИмяСвойства) Тогда
			ТекстСообщения = ТекстСообщения + ОбщиеФункции._СтрШаблон_("В настройках обмена не найдено свойство '%1'", ИмяСвойства);
		КонецЕсли; 
		
	КонецЦикла;
	
	Если ТекстСообщения <> "" Тогда
		ВызватьИсключение ТекстСообщения;
	КонецЕсли; 
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаНоменклатурыИзФайла(ТаблицаДанных) Экспорт
	
	ОбновитьНоменклатуру(ОпределитьТребуемыеДействияПоРаботеСНоменклатурой(ТаблицаДанных));
	
КонецПроцедуры
 
#Область ОБЩИЕ

Процедура СформироватьТаблицуОсновныхКарточек()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	влЗапрос.Номенклатура,
	|	влЗапрос.ОсновнаяНоменклатура,
	|	влЗапрос.Номенклатура.Артикул КАК Артикул
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(ЗначенияСвойствОбъектов.Объект КАК Справочник.Номенклатура) КАК Номенклатура,
	|		ВЫРАЗИТЬ(ЗначенияСвойствОбъектов.Значение КАК Справочник.Номенклатура) КАК ОсновнаяНоменклатура
	|	ИЗ
	|		РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|	ГДЕ
	|		ЗначенияСвойствОбъектов.Свойство = &Свойство_ОсновнаяКарточкаНоменклатуры) КАК влЗапрос
	|ГДЕ
	|	влЗапрос.ОсновнаяНоменклатура В ИЕРАРХИИ(&ГруппаНоменклатуры)";
	
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", СвойстваПартнера.ГруппаНоменклатуры); 
	Запрос.УстановитьПараметр("Свойство_ОсновнаяКарточкаНоменклатуры", ПланыВидовХарактеристик.СвойстваОбъектов.ПолучитьСвойствоПоИмени("ОсновнаяКарточкаНоменклатуры")); 
	
	ТаблицаОсновныхКарточекНоменклатуры = Запрос.Выполнить().Выгрузить();
	
	ТаблицаОсновныхКарточекНоменклатуры.Индексы.Добавить("ОсновнаяНоменклатура");
	
КонецПроцедуры
 
Процедура КонвертироватьТоварыВыкупаВТоварыДистрибьютора(Товары)
	
	Если ТаблицаОсновныхКарточекНоменклатуры = Неопределено Тогда
		СформироватьТаблицуОсновныхКарточек();
	КонецЕсли; 
	
	Для каждого Элемент Из Товары Цикл
		
		СтрОсновная = ТаблицаОсновныхКарточекНоменклатуры.Найти(Элемент.Номенклатура, "ОсновнаяНоменклатура");
		
		Если СтрОсновная = Неопределено Тогда
			Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось конвертировать товар выкупа '%1' в товар дистрибьютора по причине: у товара дистрибьютора не заполнено свойство 'Основная карточка номенклатуры' или товара дистрибьютора не существует", Элемент.Номенклатура), СтатусСообщения.Внимание);
			Продолжить;
		КонецЕсли; 
		
		Элемент.Номенклатура = СтрОсновная.Номенклатура;
		
	КонецЦикла; 
	
КонецПроцедуры
 
Функция ОпределитьГруппуДоставки(СтруктурнаяЕдиницаКонтрагента)
	
	ГруппаДоставки = Справочники.ГруппыДоставкиЗаказовПокупателей.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(СтруктурнаяЕдиницаКонтрагента) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КонтактнаяИнформация.Поле4
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Объект = &СтруктурнаяЕдиницаКонтрагента
		|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресСтруктурнойЕдиницыКонтрагента)";
		
		Запрос.УстановитьПараметр("СтруктурнаяЕдиницаКонтрагента", СтруктурнаяЕдиницаКонтрагента);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Поле4) Тогда
				ГруппаДоставки = Справочники.ГруппыДоставкиЗаказовПокупателей.НайтиПоКоду("Ц00099");
			Иначе 
				ГруппаДоставки = Справочники.ГруппыДоставкиЗаказовПокупателей.НайтиПоКоду("Ц00020");
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат ГруппаДоставки;
	
КонецФункции

Процедура СформироватьТаблицуЕдиницПоклажедателя()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	влЗапрос.Объект,
	|	влЗапрос.Значение.КодовоеОбозначение КАК ЕдИзмПоклажедателя
	|ИЗ
	|	(ВЫБРАТЬ
	|		СвойстваОбъектов.Объект КАК Объект,
	|		СвойстваОбъектов.Значение КАК Значение
	|	ИЗ
	|		Справочник.Номенклатура КАК Номенклатура
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваОбъектов
	|			ПО Номенклатура.Ссылка = СвойстваОбъектов.Объект
	|	ГДЕ
	|		Номенклатура.Ссылка В ИЕРАРХИИ(&ГруппаНоменклатуры)
	|		И Номенклатура.ПометкаУдаления = ЛОЖЬ
	|		И Номенклатура.ЭтоГруппа = ЛОЖЬ) КАК влЗапрос";
	
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", СвойстваПартнера.ГруппаНоменклатуры);
	
	ТаблицаЕдиницИзмеренийПоклажедателя = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры
 
Процедура СформироватьТаблицаТоваровДистрибьютора()
	
	ВнСистема = СвойстваПартнера.ВнешняяСистемаОбмена3PD;
	
	Если НЕ ЗначениеЗаполнено(ВнСистема) Тогда
		
		Сообщить("У партнера не найдено свойство 'ВнешняяСистемаОбмена3PD'. Создание дублей для дистрибютора невозможно");
		Возврат;
		
	КонецЕсли;
	
	Контракты = Справочники.ВнешниеСистемы.ПолучитьЗначенияКлюча(ВнСистема, "КонтрактДляОстатков");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	Номенклатура.Артикул
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ДистрибьюторскийКонтракт В(&МассивКонтрактов)
	|	И Номенклатура.ПометкаУдаления = ЛОЖЬ
	|	И Номенклатура.ЭтоГруппа = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("МассивКонтрактов", Контракты); 
	
	ТаблицаТоваровДистрибьютора = Запрос.Выполнить().Выгрузить();
	
	ТаблицаТоваровДистрибьютора.Индексы.Добавить("Артикул");
	
КонецПроцедуры
 
Процедура ЗаполнитьТаблицуНовыхЗаявокНаОбновлениеСтатусовПоставки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Объект,
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор,
	|	ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Текст КАК СТРОКА(36)) КАК НомерТранспортировки
	|ИЗ
	|	РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций
	|ГДЕ
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор В (""shipmentupdate_004"", ""shipmentupdate_005"")
	|	И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыЗаявок.Новая)";
	
	ТаблицаНовыхЗаявокНаОбновлениеСтатусовПоставки = Запрос.Выполнить().Выгрузить();
	ТаблицаНовыхЗаявокНаОбновлениеСтатусовПоставки.Колонки.Индекс(ТаблицаНовыхЗаявокНаОбновлениеСтатусовПоставки.Колонки.НомерТранспортировки);
	
КонецПроцедуры

Функция ОпределитьЕдиницуИзмеренияПепси(ЕдиницаИзмерения, Владелец)
	
	Если ТаблицаЕдиницИзмеренийПоклажедателя = Неопределено Тогда
		СформироватьТаблицуЕдиницПоклажедателя();
	КонецЕсли; 
	
	ЕдиницаИзмеренияПепси = "";
	
	СтрЕдиницаИзмеренияПоклажедателя = ТаблицаЕдиницИзмеренийПоклажедателя.Найти(Владелец, "Объект");
	
	Если СтрЕдиницаИзмеренияПоклажедателя <> Неопределено Тогда
		ЕдиницаИзмеренияПепси = СтрЕдиницаИзмеренияПоклажедателя.ЕдИзмПоклажедателя;
	Иначе
		
		Если ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код = "796" Тогда
			ЕдиницаИзмеренияПепси = "EA";
		ИначеЕсли ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код = "166" Тогда
			ЕдиницаИзмеренияПепси = "KG";
		Иначе
			ЕдиницаИзмеренияПепси = "CS";
		КонецЕсли;
	
	КонецЕсли; 
		
	Возврат ЕдиницаИзмеренияПепси;
	
КонецФункции

Процедура ЗаписатьСвойствоИЗначениеJSON(Запись, ИмяСвойства, пЗначение = Неопределено) Экспорт
	
	Запись.ЗаписатьИмяСвойства(ИмяСвойства);
	Если пЗначение <> Неопределено Тогда
		Если ТипЗнч(пЗначение) = Тип("Дата") Тогда
			Запись.ЗаписатьЗначение(ЗаписатьДатуJSON(пЗначение, ФорматДатыJSON.ISO));
		ИначеЕсли ТипЗнч(пЗначение) = Тип("Число") Тогда
			Запись.ЗаписатьЗначение(Формат(пЗначение, "ЧРД=.; ЧН=0; ЧГ=0"));
		Иначе
			Запись.ЗаписатьЗначение(Строка(пЗначение));
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

Функция ПрочитатьДату(ДатаВСтроке, Формат)
	
	ПустаяДата = Дата(1,1,1);
	
	Если НЕ ЗначениеЗаполнено(ДатаВСтроке) Тогда
		Возврат ПустаяДата;
	КонецЕсли;

	// В интерфейсах дата приходит в разных форматах.
	Если Формат = ФорматДатыJSON.ISO Тогда
		Возврат ПрочитатьДатуJSON(ДатаВСтроке, Формат)
	ИначеЕсли Формат = "yyyyMMdd" Тогда
		Возврат Дата(ДатаВСтроке);
	ИначеЕсли Формат = "dd.MM.yyyy" Тогда	
		Возврат Дата(ДатаВСтроке+" 00:00:00");
	Иначе
		Возврат ПустаяДата;
	КонецЕсли; 
	
КонецФункции

Функция ПреобразоватьСтрокуТабЗначВСтруктуру(СтрокаТабЗнач, КолонкиТабЗнач)
	
	СписокСвойств = "";
	
	Для каждого Колонка Из КолонкиТабЗнач Цикл
		СписокСвойств = СписокСвойств + ?(СписокСвойств = "", "", ",") + Колонка.Имя;
	КонецЦикла;
	
	МассивСвойств = ОбщиеФункции.РазложитьСтрокуВМассив(СписокСвойств, ",");
	
	СтруктураСтроки = Новый Структура;
	
	Для каждого Свойство Из МассивСвойств Цикл
		СтруктураСтроки.Вставить(Свойство, СтрокаТабЗнач[Свойство]);
	КонецЦикла;
	
	СтруктураСтроки.Вставить("Партнер", Партнер);
	
	Возврат СтруктураСтроки;
	
КонецФункции

Функция СформироватьСтруктуруМетода(ОписаниеМетода)
	
	СтруктураМетода = Новый Структура;
	СоответствиеПараметров = Новый Соответствие;
	СоответствиеЗаголовков = Новый Соответствие;
	
	Для каждого Реквизит Из ОписаниеМетода.ДополнительныеРеквизиты Цикл
		
		Если Найти(ВРЕГ(Реквизит.ИмяРеквизита), "ПАРАМЕТР") > 0 Тогда
			
			СоответствиеПараметров.Вставить(Реквизит.ЗначениеСтрока, Реквизит.Значение);
			
			Продолжить;
			
		ИначеЕсли Найти(ВРЕГ(Реквизит.ИмяРеквизита), "ЗАГОЛОВОК") > 0 Тогда
			
			СоответствиеЗаголовков.Вставить(Реквизит.ЗначениеСтрока, Реквизит.Значение);
			
			Продолжить;
			
		КонецЕсли;
				
		СтруктураМетода.Вставить(Реквизит.ИмяРеквизита, ?(ЗначениеЗаполнено(Реквизит.Значение), Реквизит.Значение, Реквизит.ЗначениеСтрока));
		
	КонецЦикла;
	
	СтруктураМетода.Вставить("Параметры", СоответствиеПараметров);
	СтруктураМетода.Вставить("Заголовки", СоответствиеЗаголовков);
	
	Возврат СтруктураМетода;
	
КонецФункции

Функция СформироватьТекстИсключения(АдресРесурса, ОписаниеОшибки)
	
	ПолныйПутьКРесурсу = ОбщиеФункции._СтрШаблон_("https://%1%2", Соединение.Сервер, АдресРесурса);
	
	Возврат ОбщиеФункции._СтрШаблон_("Ошибка при выполнении запроса к ресурсу '%1':%2", ПолныйПутьКРесурсу, ОписаниеОшибки);
	
КонецФункции

Процедура СоздатьЗапросДляМетода(СтруктураМетода, Запрос, ДопПараметрыЗапроса = Неопределено)
	// Укажаем обязательные параметры
	ПараметрыЗапроса = ОбщиеФункции._СтрШаблон_("?requiredTimeStamp.greaterThan=%1", Формат(СвойстваПартнера.ДатаПоследнегоОбмена - 25200, "ДФ='yyyy-MM-dd HH:mm:ss'"));
	// Укажаем обязательные заголовки
	Заголовки = Новый Соответствие;
	СтруктураМетода.Заголовки.Вставить("Plant", СвойстваПартнера.КодДистрибьютора);
	СтруктураМетода.Заголовки.Вставить("Host", СвойстваПартнера.АдресСервиса);
	СтруктураМетода.Заголовки.Вставить("Authorization", СвойстваПартнера.Токен);
	
	Если СтруктураМетода.ТипМетода = "GET" Тогда
		
		Для каждого КлючИЗначение Из СтруктураМетода.Параметры Цикл
			ПараметрыЗапроса = ПараметрыЗапроса + ОбщиеФункции._СтрШаблон_("%1%2=%3",
				?(ПараметрыЗапроса = "?", "", "&"), КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		// Замена обязательных параметров
		Если ДопПараметрыЗапроса <> Неопределено 
			И ТипЗнч(ДопПараметрыЗапроса) = Тип("Структура") Тогда
			
			ПараметрыЗапроса = "?";
			
			Для каждого КлючИЗначение Из ДопПараметрыЗапроса Цикл
				ПараметрыЗапроса = ПараметрыЗапроса + ОбщиеФункции._СтрШаблон_("%1%2=%3",
					?(ПараметрыЗапроса = "?", "", "&"), КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЦикла;
			
		КонецЕсли; 
			
	Иначе
		ПараметрыЗапроса = "";
	КонецЕсли; 

	Запрос = Новый HTTPЗапрос;
	Запрос.АдресРесурса = СтруктураМетода.АдресРесурса + ПараметрыЗапроса;
	Запрос.Заголовки = СтруктураМетода.Заголовки;
	
КонецПроцедуры
 
Функция ПолучитьДанныеИзОтвета(Ответ, АдресРесурса)
	
	Перем РезультатЗапроса;
	
	Если НЕ ЗначениеЗаполнено(Ответ.ПолучитьТелоКакСтроку()) Тогда
		Возврат Новый Структура;
	КонецЕсли; 
	
	Попытка
		
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		РезультатЗапроса = ПрочитатьJSON(Чтение);
		Чтение.Закрыть();
		
	Исключение
		ВызватьИсключение ОбщиеФункции._СтрШаблон_("При обработке ответа по ресурсу '%1' произошла ошибка: %2",
			АдресРесурса, ОписаниеОшибки());
	КонецПопытки;
	
	Возврат РезультатЗапроса
	
КонецФункции
 
Процедура ПрочитатьСвойстваПартнера() Экспорт
	
	Если СвойстваПартнера <> Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СвойстваПартнера = РегистрыСведений.ДополнительныеРеквизиты_3PL.ПолучитьСвойстваОбъекта(Партнер);
	
	Если СвойстваПартнера.Количество() = 0 Тогда
		СвойстваПартнера = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПрочитатьМетодыОбменаСПартнером()
	
	Если МетодыОбменаСПартнером <> Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	МетодыОбменаСПартнером = РегистрыСведений.ДополнительныеРеквизиты_3PL.ПолучитьМетодыОбмена(Партнер);
	
	Если МетодыОбменаСПартнером.Количество() = 0 Тогда
		МетодыОбменаСПартнером = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

Процедура УстановитьСоединение()
	
	ПрочитатьСвойстваПартнера();
	
	ПрочитатьМетодыОбменаСПартнером();
	
	ПроверкаЗаполнения();
	
	ОбновитьТокенДоступа();
	
	Защита = Новый ЗащищенноеСоединениеOpenSSL(Новый СертификатКлиентаWindows, Новый СертификатыУдостоверяющихЦентровWindows);

	Попытка
		Соединение = Новый HTTPСоединение(СвойстваПартнера.АдресСервиса,, СвойстваПартнера.Пользователь, СвойстваПартнера.Пароль,,60, Защита);
	Исключение
		ВызватьИсключение ОбщиеФункции._СтрШаблон_("Не удалось подключиться к сервису '%1' по причине: %2", СвойстваПартнера.АдресСервиса, ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

Функция ДобавитьЛидирующиеНулиВКодТовара(Знач Код)
	// Почему-то у ПепсиКо одни интерфейсы работают с кодами без 0, а другие их требуют
	ДлинаКода = 18;
	
	ТекДлинаКода = СтрДлина(Код);
	
	Для Сч = 1 По ДлинаКода - ТекДлинаКода Цикл
		Код = "0" + Код;
	КонецЦикла;
	
	Возврат Код
	
КонецФункции

Функция УдалитьЛидирущиеНулиИзКодаТовара(Знач Код)
	
	Возврат Формат(Число(Код), "ЧГ=0");
	
КонецФункции

Процедура ТестированиеИнтерфейсов()
	
	Текст = Новый ТекстовыйДокумент;
	Текст.Прочитать("D:\1С (Разное)\МОЗАЙКА\prints.json");
	
	РезультатЗапроса = Неопределено;
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Текст.ПолучитьТекст());
	РезультатЗапроса = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();
	//
	ПрочитатьСвойстваПартнера();
	//
	ПроверкаЗаполнения();
	
	СвойстваПартнера.ТокенПечати = "sig=NL6OuThR%2F14ux7kR7guPeScTtjtJHUCXYDwscx0MIq0%3D&st=2021-09-07T08%3A00%3A18Z&se=2021-09-07T12%3A00%3A18Z&sv=2019-02-02&sp=rl&sr=c";
	
	ТаблицаПечФорм = СформироватьТаблицуПечФорм();
	
	ДобавитьСтрокиВТаблицуПечФорм(ТаблицаПечФорм, РезультатЗапроса);
	
	ЗагрузитьПечатныеФормы(ТаблицаПечФорм);
		
КонецПроцедуры

Процедура Тест_ПолучитьДокументы()
	
	Партнер = Справочники.КонтурEDI_ДополнительныеСправочники.НайтиПоНаименованию("3PL_Pepsico_Омск", Истина);
	
	Текст = Новый ТекстовыйДокумент;
	Текст.Прочитать("D:\1С (Разное)\МОЗАЙКА\docs.json");
	
	РезультатЗапроса = Неопределено;
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Текст.ПолучитьТекст());
	Данные = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();
	
	ПрочитатьСвойстваПартнера();
	
	ПроверкаЗаполнения();
	
	СформироватьТаблицаТоваровПоклажедателя();
	
	СоздатьДокументы(ОпределитьТребуемыеДействияПоРаботеСДокументами(ПреобразоватьДанныеПоДокументамВТаблицуЗначений(Данные)));
	
КонецПроцедуры
 
Процедура УдалениеТестовыхДанных()
	
	ВыборкаДляУдаления = Справочники.Номенклатура.Выбрать(СвойстваПартнера.ГруппаНоменклатуры);
	
	КолУдаленных = 1;
	Пока ВыборкаДляУдаления.Следующий() Цикл
		
		Если ВыборкаДляУдаления.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли; 
		
		#Если Клиент Тогда
			Состояние("Удалено: "+КолУдаленных);
		#КонецЕсли
		
		ВыборкаЕдиницы = Справочники.ЕдиницыИзмерения.Выбрать(, ВыборкаДляУдаления.Ссылка);
		
		Пока ВыборкаЕдиницы.Следующий() Цикл
			
			Попытка
				ЕдиницаОбъект = ВыборкаЕдиницы.Ссылка.ПолучитьОбъект();
				ЕдиницаОбъект.Удалить();
			Исключение
				Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось удалить единицы измерения по товару '%1' по причине: %2", ВыборкаДляУдаления.Ссылка, ОписаниеОшибки()));
			КонецПопытки;
			
		КонецЦикла;
		
		Товар = ВыборкаДляУдаления.Ссылка.ПолучитьОбъект();
		Товар.Удалить();
		
		КолУдаленных = КолУдаленных+1;
		
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти 

#Область ПЕЧАТНЫЕ_ФОРМЫ

Процедура ЗагрузитьПечатныеФормы(ТаблицаПечФорм)
	
	Защита = Новый ЗащищенноеСоединениеOpenSSL(Новый СертификатКлиентаWindows, Новый СертификатыУдостоверяющихЦентровWindows);
	
	Попытка
		СоединениеДляПолученияПечФорм = Новый HTTPСоединение(СвойстваПартнера.АдресСервисаПечати,, , ,,600, Защита);
	Исключение
		ВызватьИсключение ОбщиеФункции._СтрШаблон_("Не удалось подключиться к сервису '%1' по причине: %2", СвойстваПартнера.АдресСервиса, ОписаниеОшибки());
	КонецПопытки;
	
	Запрос = Новый HTTPЗапрос;
	Запрос.Заголовки = Новый Соответствие;
	
	Для каждого СтрТаб Из ТаблицаПечФорм Цикл
		
		ПечФорма = Новый Файл(СтрТаб.ПутьКФайлу);
		
		Если ПечФорма.Существует() Тогда
			Продолжить;
		КонецЕсли; 
		
		КаталогПоставки = Новый Файл(ОбщиеФункции._СтрШаблон_("%1\%2", СвойстваПартнера.КаталогХраненияПечатныхФорм, СтрТаб.НомерПоставки));
		
		Если НЕ КаталогПоставки.Существует() Тогда
			СоздатьКаталог(КаталогПоставки.ПолноеИмя);
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(СтрТаб.НомерВходящегоДокумента) Тогда
			
			КаталогЗаказа = Новый Файл(ОбщиеФункции._СтрШаблон_("%1\%2\%3", СвойстваПартнера.КаталогХраненияПечатныхФорм, СтрТаб.НомерПоставки, СтрТаб.НомерВходящегоДокумента));
			
			Если НЕ КаталогЗаказа.Существует() Тогда
				СоздатьКаталог(КаталогЗаказа.ПолноеИмя);
			КонецЕсли; 
			
		КонецЕсли;  
		
		Попытка
			
			Запрос.АдресРесурса = СтрТаб.Ссылка + "?" +СвойстваПартнера.ТокенПечати;
		
			Ответ = СоединениеДляПолученияПечФорм.Получить(Запрос);
			
			Если Ответ.КодСостояния >= 200 И Ответ.КодСостояния < 300 Тогда
				
				ДвоичныеДанные = Ответ.ПолучитьТелоКакДвоичныеДанные();
				ДвоичныеДанные.Записать(СтрТаб.ПутьКФайлу);
				
			Иначе
				
				ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
				
			КонецЕсли;
			
		Исключение
			ВызватьИсключение СформироватьТекстИсключения(Запрос.АдресРесурса, ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ДобавитьСтрокиВТаблицуПечФорм(ТаблицаПечФорм, Данные)
	
	КоличествоЭлементов = Данные.Количество();
	
	КаталогХранения = СвойстваПартнера.КаталогХраненияПечатныхФорм;
	
	Для Сч = 0 По КоличествоЭлементов-1 цикл
		
		Элемент = Данные.Получить(Сч);
		
		НомерПоставки = Элемент.shipmentNumber;
		
		НомерВходящегоДокумента = ?(Элемент.deliveryNumber = "none", "", Элемент.deliveryNumber);
		
		Состав = Элемент.printDetails;
		
		Для каждого ДетальнаяЗапись Из Состав Цикл
			
			НовСтр = ТаблицаПечФорм.Добавить();
			НовСтр.НомерПоставки 			= НомерПоставки;
			НовСтр.НомерВходящегоДокумента 	= НомерВходящегоДокумента;
			НовСтр.Ссылка 					= СтрЗаменить(ДетальнаяЗапись.fileDownloadUri, "https://"+СвойстваПартнера.АдресСервисаПечати, "");
			НовСтр.ПутьКФайлу 				= ОбщиеФункции._СтрШаблон_("%1\%2\%3\%4_%5(%6).pdf", КаталогХранения, НомерПоставки, НомерВходящегоДокумента, 
				ДетальнаяЗапись.printOrder, ДетальнаяЗапись.fileName, ДетальнаяЗапись.numOfCopies);
			НовСтр.Токен 					= СвойстваПартнера.ТокенПечати;
			
		КонецЦикла; 
		
	КонецЦикла;
	
КонецПроцедуры
 
Функция СформироватьТаблицуПечФорм()
	
	ТабЗнач = Новый ТаблицаЗначений;
	ТабЗнач.Колонки.Добавить("НомерПоставки", 			Новый ОписаниеТипов("Строка"));
	ТабЗнач.Колонки.Добавить("НомерВходящегоДокумента", Новый ОписаниеТипов("Строка"));
	ТабЗнач.Колонки.Добавить("Ссылка", 					Новый ОписаниеТипов("Строка"));
	ТабЗнач.Колонки.Добавить("ПутьКФайлу", 				Новый ОписаниеТипов("Строка"));
	ТабЗнач.Колонки.Добавить("Токен", 					Новый ОписаниеТипов("Строка"));
	
	Возврат ТабЗнач;
	
КонецФункции
 
Функция ПолучитьЗаявкиНаЗагрузкуПечатныхФорм()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Объект,
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Текст КАК НомерПоставки
	|ИЗ
	|	РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций
	|ГДЕ
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Источник = &Приемник
	|	И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Приемник = ""1С""
	|	И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Статус = &Статус
	|   И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор = ""prints""";
	
	Запрос.УстановитьПараметр("Приемник", Строка(Партнер));
	Запрос.УстановитьПараметр("Статус", Справочники.СтатусыЗаявок.Новая); 
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура СоздатьЗаявкуНаПолучениеПечатныхФорм(Объект, НомерПоставки)
	
	РегистраторЗаписи = Документы.РегистраторЗаписи.СоздатьНовыйДокумент("" + Объект);
	
	_3PLСервер.ДобавитьЗаписьПоДокументуВЖурналОпераций(РегистраторЗаписи, Строка(Партнер), "1С",
		Справочники.СтатусыЗаявок.Новая, "prints", ТекущаяДатаСеанса(),, НомерПоставки);
	
КонецПроцедуры

#КонецОбласти 

#Область МАРШРУТЫ

Функция ПолучитьДанныеПоМаршрутуДляЗаполнения(ДокументСсылка)
	
	МассивКодовСобственныхАрендодателей = ОбщиеФункции.РазложитьСтрокуВМассив(СвойстваПартнера.СобственныеАрендодатели, ";");
	
	Список_СобственныеАрендодатели = Новый СписокЗначений;
	
	Для каждого Элемент Из МассивКодовСобственныхАрендодателей Цикл
		Список_СобственныеАрендодатели.Добавить(Справочники.Контрагенты.НайтиПоКоду(Элемент));
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокМаршрут.Ссылка КАК МаршрутЭкспедитора,
	|	ВЫБОР
	|		КОГДА ДокМаршрут.Транспорт.Арендодатель В (&СобственныеАрендодатели)
	|			ТОГДА ""I""
	|		ИНАЧЕ ""E""
	|	КОНЕЦ КАК ВидАрендныхОтношений,
	|	""3PLOCH"" КАК ТехническоеИмяМаршрута,
	|	ЕСТЬNULL(СвойстваТранспорта.Значение, """") КАК КодТранспортногоСредства,
	|	СпрТранспорт.Госномер КАК НаименованиеТранспорта,
	|	ЕСТЬNULL(СвойстваФизЛица.Значение, """") КАК КодСотрудникаСАП,
	|	ДокМаршрут.Экспедитор КАК ИмяСотрудника,
	|	ДокМаршрут.ДлинаМаршрута КАК ДлинаМаршрута,
	|	"""" КАК КодКассира,
	|	ДокМаршрут.НачалоМаршрута КАК ВремяНачалоМаршрута,
	|	ДокМаршрут.ОкончаниеМаршрута КАК ВремяОкончанияМаршрута,
	|	КОЛИЧЕСТВО(МаршрутДокументыОснования.ДокументОснование) КАК КоличествоДокументовМаршрута,
	|	МаршрутДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА МаршрутДокументыОснования.НомерТочкиРазгрузки > 0
	|			ТОГДА МаршрутДокументыОснования.НомерТочкиРазгрузки - 1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК НомерТочкиРазгрузки,
	|	""SIT"" КАК ТипОстановки,
	|	ЕСТЬNULL(СвойстваТорговыхТочек.Значение, """") КАК КодГрузополучателя,
	|	ЕСТЬNULL(СвойстваЗаявокПоклажедателя.Значение, """") КАК НомерВходящегоДокумента
	|ИЗ
	|	Документ.МаршрутЭкспедитора КАК ДокМаршрут
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутЭкспедитора.ДокументыОснования КАК МаршрутДокументыОснования
	|		ПО ДокМаршрут.Ссылка = МаршрутДокументыОснования.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТранспортДоставки КАК СпрТранспорт
	|		ПО ДокМаршрут.Транспорт = СпрТранспорт.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурнаяЗаявок
	|		ПО (МаршрутДокументыОснования.ДокументОснование = ЖурнаяЗаявок.Объект)
	|			И (ЖурнаяЗаявок.Источник = &Партнер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваТранспорта
	|		ПО (СпрТранспорт.Ссылка = СвойстваТранспорта.Объект)
	|			И (СвойстваТранспорта.Свойство = &КодТранспортаСАП)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваФизЛица
	|		ПО ДокМаршрут.Экспедитор = СвойстваФизЛица.Объект
	|			И (СвойстваФизЛица.Свойство = &КодСотрудникаСАП)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваТорговыхТочек
	|		ПО (МаршрутДокументыОснования.ПунктРазгрузки = СвойстваТорговыхТочек.Объект
	|				ИЛИ МаршрутДокументыОснования.ПунктПогрузки = СвойстваТорговыхТочек.Объект)
	|			И (СвойстваТорговыхТочек.Свойство = &КодТорговойТочки)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваЗаявокПоклажедателя
	|		ПО (МаршрутДокументыОснования.ДокументОснование = СвойстваЗаявокПоклажедателя.Объект)
	|			И (СвойстваЗаявокПоклажедателя.Свойство = &НомерВходящегоДокумента)
	|ГДЕ
	|	ДокМаршрут.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокМаршрут.Ссылка,
	|	СпрТранспорт.Госномер,
	|	ДокМаршрут.Экспедитор,
	|	ДокМаршрут.ДлинаМаршрута,
	|	ДокМаршрут.НачалоМаршрута,
	|	ДокМаршрут.ОкончаниеМаршрута,
	|	МаршрутДокументыОснования.ДокументОснование,
	|	МаршрутДокументыОснования.НомерТочкиРазгрузки,
	|	ЕСТЬNULL(СвойстваЗаявокПоклажедателя.Значение, """"),
	|	ВЫБОР
	|		КОГДА ДокМаршрут.Транспорт.Арендодатель В (&СобственныеАрендодатели)
	|			ТОГДА ""I""
	|		ИНАЧЕ ""E""
	|	КОНЕЦ,
	|	ЕСТЬNULL(СвойстваТранспорта.Значение, """"),
	|	ЕСТЬNULL(СвойстваФизЛица.Значение, """"),
	|	ЕСТЬNULL(СвойстваТорговыхТочек.Значение, """")
	|
	|УПОРЯДОЧИТЬ ПО
	|	МаршрутЭкспедитора,
	|	МаршрутДокументыОснования.НомерТочкиРазгрузки
	|ИТОГИ
	|	МАКСИМУМ(ВидАрендныхОтношений),
	|	МАКСИМУМ(ТехническоеИмяМаршрута),
	|	МАКСИМУМ(КодТранспортногоСредства),
	|	МАКСИМУМ(НаименованиеТранспорта),
	|	МАКСИМУМ(КодСотрудникаСАП),
	|	МАКСИМУМ(ИмяСотрудника),
	|	МАКСИМУМ(ДлинаМаршрута),
	|	МАКСИМУМ(КодКассира),
	|	МАКСИМУМ(ВремяНачалоМаршрута),
	|	МАКСИМУМ(ВремяОкончанияМаршрута),
	|	СУММА(КоличествоДокументовМаршрута),
	|	МАКСИМУМ(ДокументОснование),
	|	МАКСИМУМ(ТипОстановки),
	|	МАКСИМУМ(КодГрузополучателя)
	|ПО
	|	МаршрутЭкспедитора,
	|	НомерТочкиРазгрузки";
	
	Запрос.УстановитьПараметр("КодСотрудникаСАП", 			СвойстваПартнера.Свойство_КодСотрудникаСАП);
	Запрос.УстановитьПараметр("КодТорговойТочки", 			СвойстваПартнера.Свойство_КодГрузополучателя);
	Запрос.УстановитьПараметр("КодТранспортаСАП", 			СвойстваПартнера.Свойство_КодТранспортаСАП);
	Запрос.УстановитьПараметр("НомерВходящегоДокумента", 	СвойстваПартнера.Свойство_НомерВходящегоДокумента);
	Запрос.УстановитьПараметр("СобственныеАрендодатели", 	Список_СобственныеАрендодатели);
	Запрос.УстановитьПараметр("Ссылка", 					ДокументСсылка);
	Запрос.УстановитьПараметр("Партнер", 					Строка(Партнер));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаМаршрутЭкспедитора = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Возврат ВыборкаМаршрутЭкспедитора;
		
КонецФункции

Функция СформироватьТелоЗапросаДляОтправкиМаршрута(ДокументСсылка)
	
	 ТелоЗапроса = "";
	 
	 ВыборкаПоМаршруту = ПолучитьДанныеПоМаршрутуДляЗаполнения(ДокументСсылка);
	 ВыборкаПоМаршруту.Следующий();
	 
	 МассивОшибок = Новый Массив;
	 
	 Если НЕ ЗначениеЗаполнено(ВыборкаПоМаршруту.КодТранспортногоСредства) Тогда
	 	 МассивОшибок.Добавить(ОбщиеФункции._СтрШаблон_("У транспорта '%1' не заполнен код транс. средства SAP", ВыборкаПоМаршруту.НаименованиеТранспорта));
	 КонецЕсли;
	 
	 Если НЕ ЗначениеЗаполнено(ВыборкаПоМаршруту.КодСотрудникаСАП) Тогда
	 	 МассивОшибок.Добавить(ОбщиеФункции._СтрШаблон_("У сотрудника '%1' не заполнен код сотрудника SAP", ВыборкаПоМаршруту.ИмяСотрудника));
	 КонецЕсли;
	 
	 Если ЗначениеЗаполнено(МассивОшибок) Тогда
		 
		 Сообщить(ОбщиеФункции._СтрШаблон_("Маршрут '%1' не будет отправлен по причине:", ДокументСсылка), СтатусСообщения.ОченьВажное);
		 
		 Для каждого Элемент Из МассивОшибок Цикл
			 Сообщить(Элемент, СтатусСообщения.Внимание);
		 КонецЦикла;
		 
		 Возврат ТелоЗапроса;
		 
	 КонецЕсли; 
	 
	 ВремяНачалаМаршрута =		ДокументСсылка.ДатаЭкспедиции + (ВыборкаПоМаршруту.ВремяНачалоМаршрута - Дата(1,1,1));
	 Если НЕ ЗначениеЗаполнено(ВыборкаПоМаршруту.ВремяНачалоМаршрута) Тогда
		 ВремяНачалаМаршрута = ДокументСсылка.ДатаЭкспедиции + (СвойстваПартнера.ВремяНачалаМаршрута - Дата(1,1,1));
	 КонецЕсли; 
	
	 ВремяОкончанияМаршрута = 	ДокументСсылка.ДатаЭкспедиции + (ВыборкаПоМаршруту.ВремяОкончанияМаршрута - Дата(1,1,1));
	 Если НЕ ЗначениеЗаполнено(ВыборкаПоМаршруту.ВремяОкончанияМаршрута) Тогда
		 ВремяОкончанияМаршрута = ДокументСсылка.ДатаЭкспедиции + (СвойстваПартнера.ВремяОкончанияМаршрута - Дата(1,1,1));
	 КонецЕсли;
	 
	 Запись = Новый ЗаписьJSON;
	 Запись.УстановитьСтроку();
	 Запись.ЗаписатьНачалоОбъекта();
	 
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "locationID", 	СвойстваПартнера.ТехническоеИмяМестаОтгрузки);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "plantNumber", 	СвойстваПартнера.КодДистрибьютора);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "interfaceName", "DeliveryRouting");
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "shipmentType", 	ВыборкаПоМаршруту.ВидАрендныхОтношений);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "routeCode", 	ВыборкаПоМаршруту.ТехническоеИмяМаршрута);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "equipmentID", 	ВыборкаПоМаршруту.КодТранспортногоСредства);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "equipmentName", СтрЗаменить(ВыборкаПоМаршруту.НаименованиеТранспорта, " ", ""));
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "employeeID", 	ВыборкаПоМаршруту.КодСотрудникаСАП);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "employeeName", 	ВыборкаПоМаршруту.ИмяСотрудника);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "employee2ID", 	ВыборкаПоМаршруту.КодКассира);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "distance", 		ВыборкаПоМаршруту.ДлинаМаршрута);
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "startTime", 	ЗаписатьДатуJSON(ВремяНачалаМаршрута, ФорматДатыJSON.ISO));
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "endTime", 		ЗаписатьДатуJSON(ВремяОкончанияМаршрута, ФорматДатыJSON.ISO));
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "exti2", 		ОбщиеФункции._СтрШаблон_("%1_%2", ДокументСсылка.Номер, Формат(ДокументСсылка.Дата, "ДФ=yyyyMMdd")));
		 
		 ВыборкаПоНомерамРазгрузки = ВыборкаПоМаршруту.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		 
		 ЗаписатьСвойствоИЗначениеJSON(Запись, "stops");
		 СреднееВремяПосещения = СвойстваПартнера.СреднееВремяПосещения;
		 Если ЗначениеЗаполнено(ВремяНачалаМаршрута) И ЗначениеЗаполнено(ВремяОкончанияМаршрута) Тогда
		 	 СреднееВремяПосещения = (ВремяОкончанияМаршрута - ВремяНачалаМаршрута - СреднееВремяПосещения) / ВыборкаПоМаршруту.КоличествоДокументовМаршрута;
		 КонецЕсли;
		 
		 ДатаПосещения = ВремяНачалаМаршрута + СреднееВремяПосещения;
		 Запись.ЗаписатьНачалоМассива();
		 Пока ВыборкаПоНомерамРазгрузки.Следующий() Цикл
			 
			     Запись.ЗаписатьНачалоОбъекта();
				 
				 	ЗаписатьСвойствоИЗначениеJSON(Запись, "stopIx", 		ВыборкаПоНомерамРазгрузки.НомерТочкиРазгрузки);
					ЗаписатьСвойствоИЗначениеJSON(Запись, "arrivalTime", 	ЗаписатьДатуJSON(ДатаПосещения, ФорматДатыJSON.ISO));
					ЗаписатьСвойствоИЗначениеJSON(Запись, "departureTime", 	ЗаписатьДатуJSON(ДатаПосещения, ФорматДатыJSON.ISO));
					ЗаписатьСвойствоИЗначениеJSON(Запись, "locationType", 	ВыборкаПоНомерамРазгрузки.ТипОстановки);
					ЗаписатьСвойствоИЗначениеJSON(Запись, "locationID", 	ВыборкаПоНомерамРазгрузки.КодГрузополучателя);
					
					ЗаписатьСвойствоИЗначениеJSON(Запись, "orders");
					
					ВыборкаПоДокументам = ВыборкаПоНомерамРазгрузки.Выбрать();
					
					Запись.ЗаписатьНачалоМассива();
					Пока ВыборкаПоДокументам.Следующий() Цикл
						
						Запись.ЗаписатьНачалоОбъекта();
							ЗаписатьСвойствоИЗначениеJSON(Запись, "orderNumber", ВыборкаПоДокументам.НомерВходящегоДокумента);
						Запись.ЗаписатьКонецОбъекта();
						
					КонецЦикла; 
					Запись.ЗаписатьКонецМассива();
				 
				 Запись.ЗаписатьКонецОбъекта();
				 
			     ДатаПосещения = ДатаПосещения + СреднееВремяПосещения;
				 
		 КонецЦикла; 
		 Запись.ЗаписатьКонецМассива();
	Запись.ЗаписатьКонецОбъекта();
		 
	ТелоЗапроса = Запись.Закрыть();
		 
	Возврат ТелоЗапроса;
	
КонецФункции
 
#КонецОбласти 

#Область ОСТАТКИ

Функция ПолучитьТекущиеОстатки(Склады)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗапасыНаСкладахОстатки.Номенклатура,
	|	ВЫБОР
	|		КОГДА ЗапасыНаСкладахОстатки.Качество <> ЗНАЧЕНИЕ(Справочник.Качество.Стандарт)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Качество.Брак)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Качество.Стандарт)
	|	КОНЕЦ КАК Качество,
	|	СУММА(ЗапасыНаСкладахОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.СерияНоменклатуры.НомерПартииПоставщика, """") КАК НомерПартииПоставщика,
	|	ЕСТЬNULL(МАКСИМУМ(ЗапасыНаСкладахОстатки.СерияНоменклатуры.Используется), ЛОЖЬ) КАК Используется
	|ИЗ
	|	РегистрНакопления.ЗапасыНаСкладах.Остатки(
	|			,
	|			Склад В (&Склады)
	|				И Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)) КАК ЗапасыНаСкладахОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыНаСкладахОстатки.СерияНоменклатуры.НомерПартииПоставщика,
	|	ЗапасыНаСкладахОстатки.Номенклатура,
	|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаХраненияОстатков,
	|	ВЫБОР
	|		КОГДА ЗапасыНаСкладахОстатки.Качество <> ЗНАЧЕНИЕ(Справочник.Качество.Стандарт)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Качество.Брак)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Качество.Стандарт)
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Качество,
	|	ЗапасыНаСкладахОстатки.Номенклатура.ДистрибьюторскийКонтракт";
	
	Запрос.УстановитьПараметр("Склады", Склады);
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", СвойстваПартнера.ГруппаНоменклатуры);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции
 
Функция СформироватьТаблицуСкладовПоклажедателей()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СкладыПоклажедателей3PL.Ссылка,
	|	СкладыПоклажедателей3PL.Код,
	|	СкладыПоклажедателей3PL.Наименование,
	|	СкладыПоклажедателей3PL.Партнер,
	|	СкладыПоклажедателей3PL.СсылкаНаОбъект
	|ИЗ
	|	Справочник.СкладыПоклажедателей3PL КАК СкладыПоклажедателей3PL
	|ГДЕ
	|	СкладыПоклажедателей3PL.Партнер = &Партнер
	|	И СкладыПоклажедателей3PL.СсылкаНаОбъект <> НЕОПРЕДЕЛЕНО";
	
	Запрос.УстановитьПараметр("Партнер", Партнер); 
	
	ТаблицаСкладовПоклажедателей = Запрос.Выполнить().Выгрузить();
	
КонецФункции
 
Функция СформироватьТелоЗапросаПоТекущимОстаткам()
	
	ТелоЗапроса = "";
	
	Версия = "000";
	ЛогическийСклад = СокрЛП(СвойстваПартнера.ОсновнойСклад.Код);
	Завод = СвойстваПартнера.КодДистрибьютора;
	ЛогическийСкладБлокировки = СокрЛП(СвойстваПартнера.СкладБлокировки.Код);
	КачествоДляБлокировки = СвойстваПартнера.КачествоДляБлокировки;
	
	ТаблицаОстатков = ПолучитьТекущиеОстатки(СвойстваПартнера.Склад);
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	
	Запись.ЗаписатьНачалоОбъекта();
	
	Запись.ЗаписатьИмяСвойства("inventorySnapshotHeader");
	
	Запись.ЗаписатьНачалоОбъекта();
	
	ЗаписатьСвойствоИЗначениеJSON(Запись, "interfaceName", "inventorysnapshot");
	
	Запись.ЗаписатьИмяСвойства("invSnap");
	
	Запись.ЗаписатьНачалоМассива();
	
	КодСклада = "1000";
	
	Для каждого СтрТаб Из ТаблицаОстатков Цикл
		
		СтрокаСоСкладомПоклажедателя = ТаблицаСкладовПоклажедателей.Найти(СтрТаб.Качество, "СсылкаНаОбъект");
		
		Если СтрокаСоСкладомПоклажедателя = Неопределено Тогда
			
			Сообщить(ОбщиеФункции._СтрШаблон_("По товару '%1' с качеством '%2' и серий '%3' не удалось подобрать склад поклажедателя", 
				СтрТаб.Номенклатура, СтрТаб.Качество, СтрТаб.СерияНоменклатуры), СтатусСообщения.ОченьВажное);
				
			Продолжить;
			
		Иначе
			
			КодСклада = СтрокаСоСкладомПоклажедателя.Код;
			
		КонецЕсли;
		
		Запись.ЗаписатьНачалоОбъекта();
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "skuid",         ДобавитьЛидирующиеНулиВКодТовара(СтрТаб.Номенклатура.Артикул));
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "plant",         Завод);
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "storloc",       КодСклада);
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "lot",           СтрТаб.НомерПартииПоставщика);
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "version",       Версия);
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "baseuom",       ОпределитьЕдиницуИзмеренияПепси(СтрТаб.ЕдиницаИзмерения, СтрТаб.Номенклатура));
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "availableqty",  ?(СтрТаб.Качество = Справочники.Качество.Стандарт И СтрТаб.Используется, Формат(СтрТаб.КоличествоОстаток, "ЧРД=.; ЧГ=0"), "0"));
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "blockedqty",    ?(СтрТаб.Качество <> Справочники.Качество.Стандарт И СтрТаб.Используется, Формат(СтрТаб.КоличествоОстаток, "ЧРД=.; ЧГ=0"), "0"));
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "qiqty",         ?(НЕ СтрТаб.Используется, Формат(СтрТаб.КоличествоОстаток, "ЧРД=.; ЧГ=0"), "0"));
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "wbstext",       null);
		
		Запись.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	
	Запись.ЗаписатьКонецМассива(); 
	
	Запись.ЗаписатьКонецОбъекта();
	
	Запись.ЗаписатьКонецОбъекта(); // inventorySnapshotHeader
	
	ТелоЗапроса = Запись.Закрыть();
	
	Возврат ТелоЗапроса;
	
КонецФункции

Функция СформироватьТелоЗапросаПоРасхождениямПриПересчете(ВыборкаПоТипуОперации)
	
	ВыборкаДетальныхЗаписей = ВыборкаПоТипуОперации.Выбрать();
	
	ТекстОперации = "";
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	
	Запись.ЗаписатьНачалоОбъекта();
	ЗаписатьСвойствоИЗначениеJSON(Запись, "inventoryMove");
	
	Запись.ЗаписатьНачалоОбъекта();
	ЗаписатьСвойствоИЗначениеJSON(Запись, "interfaceName", "inventorymovement");
	
	ЗаписатьСвойствоИЗначениеJSON(Запись, "invmovhead");
	Запись.ЗаписатьНачалоОбъекта();
	ЗаписатьСвойствоИЗначениеJSON(Запись, "postdate", Формат(ТекущаяДатаСеанса(), "ДФ='yyyy-MM-dd HH:mm:ss'"));
	ЗаписатьСвойствоИЗначениеJSON(Запись, "docdate", Формат(ТекущаяДатаСеанса(), "ДФ='yyyy-MM-dd HH:mm:ss'"));
	Если ВыборкаПоТипуОперации.ТипДвижения = "311" Тогда
		ЗаписатьСвойствоИЗначениеJSON(Запись, "htxt", ОбщиеФункции.ВернутьЗначениеСвойстваОбъекта(ВыборкаПоТипуОперации.Ссылка.ДокументОснование,
			СвойстваПартнера.Свойство_НомерВходящегоДокумента));
	КонецЕсли; 
	Запись.ЗаписатьКонецОбъекта();
	
	ЗаписатьСвойствоИЗначениеJSON(Запись, "invmovcode");
	Запись.ЗаписатьНачалоОбъекта();
	ЗаписатьСвойствоИЗначениеJSON(Запись, "trcode", ВыборкаПоТипуОперации.ТипОперации);
	Запись.ЗаписатьКонецОбъекта();
	
	ЗаписатьСвойствоИЗначениеJSON(Запись, "invmovdetail");
	Запись.ЗаписатьНачалоОбъекта();
	
	ЗаписатьСвойствоИЗначениеJSON(Запись, "inventorymovementsdet");
	Запись.ЗаписатьНачалоМассива();
	Пока ВыборкаДетальныхЗаписей.Следующий() цикл
		
		Если ВыборкаПоТипуОперации.ТипДвижения = "343"
			ИЛИ ВыборкаПоТипуОперации.ТипДвижения = "344" Тогда
			// newstorloc = 1000, storloc = качество не стандарт
			СкладПолучатель = _3PLСервер.ОпределитьСкладПоклажедателя(Партнер, Справочники.Качество.Стандарт);
			Склад           = _3PLСервер.ОпределитьСкладПоклажедателя(Партнер, Справочники.Качество.Брак);			
		Иначе
			Склад = 			_3PLСервер.ОпределитьСкладПоклажедателя(Партнер, ВыборкаДетальныхЗаписей.Склад);
			СкладПолучатель = 	_3PLСервер.ОпределитьСкладПоклажедателя(Партнер, ВыборкаДетальныхЗаписей.СкладПолучатель);
		КонецЕсли; 
		
		Запись.ЗаписатьНачалоОбъекта();
			ЗаписатьСвойствоИЗначениеJSON(Запись, "itemnum", 	ДобавитьЛидирующиеНулиВКодТовара(ВыборкаДетальныхЗаписей.Номенклатура.Артикул));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "lot", 		ВыборкаДетальныхЗаписей.СерияНоменклатуры.НомерПартииПоставщика);
			ЗаписатьСвойствоИЗначениеJSON(Запись, "qty", 		?(ВыборкаДетальныхЗаписей.Количество < 0, -ВыборкаДетальныхЗаписей.Количество, ВыборкаДетальныхЗаписей.Количество));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "baseuom", 	ОпределитьЕдиницуИзмеренияПепси(ВыборкаДетальныхЗаписей.ЕдиницаИзмерения, ВыборкаДетальныхЗаписей.Номенклатура));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "movtype", 	ВыборкаДетальныхЗаписей.ТипДвижения);
			ЗаписатьСвойствоИЗначениеJSON(Запись, "storloc", 	Склад.Код);
			ЗаписатьСвойствоИЗначениеJSON(Запись, "newstorloc", СкладПолучатель.Код);
			ЗаписатьСвойствоИЗначениеJSON(Запись, "plant", 		СвойстваПартнера.КодДистрибьютора);
			ЗаписатьСвойствоИЗначениеJSON(Запись, "costcenter", СвойстваПартнера.КостЦентр);
		Запись.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	Запись.ЗаписатьКонецМассива();

	Запись.ЗаписатьКонецОбъекта();
	
	Запись.ЗаписатьКонецОбъекта();
	
	Запись.ЗаписатьКонецОбъекта();
	
	ТекстОперации = Запись.Закрыть();
	
	Возврат ТекстОперации;
	
КонецФункции

Функция ПолучитьДанныеПоДвижениямКорректировкиОстатков()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	влЗапрос.Дата,
	|	влЗапрос.Ссылка КАК Ссылка,
	|	влЗапрос.Номенклатура,
	|	влЗапрос.СерияНоменклатуры,
	|	влЗапрос.Склад,
	|	влЗапрос.СкладПолучатель,
	|	влЗапрос.ЕдиницаИзмерения,
	|	влЗапрос.Количество,
	|	влЗапрос.ТипОперации КАК ТипОперации,
	|	влЗапрос.ТипДвижения КАК ТипДвижения
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Док.Дата КАК Дата,
	|		МАКСИМУМ(Док.Ссылка) КАК Ссылка,
	|		ЗаказПоставщику.Номенклатура КАК Номенклатура,
	|		ЗаказПоставщику.СерияНоменклатуры КАК СерияНоменклатуры,
	|		&ОсновнойСклад КАК Склад,
	|		&КачествоПретензия КАК СкладПолучатель,
	|		ЗаказПоставщику.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ЗаказПоставщику.Количество - СУММА(ЕСТЬNULL(Состав.Количество, 0) * ЕСТЬNULL(Состав.Коэффициент, 0)) КАК Количество,
	|		""04"" КАК ТипОперации,
	|		""311"" КАК ТипДвижения
	|	ИЗ
	|		Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщику
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйСкладскойОрдер КАК Док
	|			ПО ЗаказПоставщику.Ссылка = Док.ДокументОснование
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриходныйСкладскойОрдер.Товары КАК Состав
	|			ПО (Док.Ссылка = Состав.Ссылка)
	|				И ЗаказПоставщику.Номенклатура = Состав.Номенклатура
	|				И ЗаказПоставщику.СерияНоменклатуры.НомерПартииПоставщика = Состав.СерияНоменклатуры.НомерПартииПоставщика
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналОпераций
	|			ПО (Док.Ссылка = ЖурналОпераций.Объект)
	|				И (ЖурналОпераций.Источник = ""1С"")
	|				И (ЖурналОпераций.Приемник = &Партнер)
	|	ГДЕ
	|		ЗаказПоставщику.Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)
	|		И ЗаказПоставщику.СерияНоменклатуры <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И Док.Проведен
	|		И (ЖурналОпераций.Объект ЕСТЬ NULL
	|				ИЛИ ЖурналОпераций.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыЗаявок.Новая))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Док.Дата,
	|		ЗаказПоставщику.Номенклатура,
	|		ЗаказПоставщику.ЕдиницаИзмерения,
	|		ЗаказПоставщику.Количество,
	|		ЗаказПоставщику.СерияНоменклатуры
	|	
	|	ИМЕЮЩИЕ
	|		ЗаказПоставщику.Количество > СУММА(ЕСТЬNULL(Состав.Количество, 0) * ЕСТЬNULL(Состав.Коэффициент, 0))) КАК влЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Док.Дата,
	|	Состав.Ссылка,
	|	Состав.Номенклатура,
	|	Состав.СерияНоменклатуры,
	|	Состав.Качество,
	|	Состав.Качество,
	|	Состав.ЕдиницаИзмерения,
	|	Состав.Количество,
	|	""03"",
	|	ВЫБОР
	|		КОГДА Состав.СерияНоменклатуры <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|				И НЕ Состав.СерияНоменклатуры.Используется
	|			ТОГДА ""Z94""
	|		ИНАЧЕ ""ZI4""
	|	КОНЕЦ
	|ИЗ
	|	Документ.СписаниеТоваров.Товары КАК Состав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеТоваров КАК Док
	|		ПО Состав.Ссылка = Док.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналОпераций
	|		ПО (Док.Ссылка = ЖурналОпераций.Объект)
	|			И (ЖурналОпераций.Источник = ""1С"")
	|			И (ЖурналОпераций.Приемник = &Партнер)
	|ГДЕ
	|	Состав.Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)
	|	И Док.Проведен
	|	И Док.ИнвентаризацияТоваровНаСкладе = НЕОПРЕДЕЛЕНО
	|	И (ЖурналОпераций.Объект ЕСТЬ NULL
	|			ИЛИ ЖурналОпераций.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыЗаявок.Новая))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Док.Дата,
	|	Состав.Ссылка,
	|	Состав.Номенклатура,
	|	Состав.СерияНоменклатуры,
	|	&ОсновнойСклад,
	|	Состав.Качество,
	|	Состав.ЕдиницаИзмерения,
	|	Состав.Количество,
	|	""03"",
	|	ВЫБОР
	|		КОГДА Состав.СерияНоменклатуры <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|				И НЕ Состав.СерияНоменклатуры.Используется
	|			ТОГДА ""Z93""
	|		ИНАЧЕ ""ZI3""
	|	КОНЕЦ
	|ИЗ
	|	Документ.ОприходованиеТоваров.Товары КАК Состав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОприходованиеТоваров КАК Док
	|		ПО Состав.Ссылка = Док.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналОпераций
	|		ПО (Док.Ссылка = ЖурналОпераций.Объект)
	|			И (ЖурналОпераций.Источник = ""1С"")
	|			И (ЖурналОпераций.Приемник = &Партнер)
	|ГДЕ
	|	Состав.Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)
	|	И Док.Проведен
	|	И Док.ИнвентаризацияТоваровНаСкладе = НЕОПРЕДЕЛЕНО
	|	И (ЖурналОпераций.Объект ЕСТЬ NULL
	|			ИЛИ ЖурналОпераций.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыЗаявок.Новая))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Док.Дата,
	|	Состав.Ссылка,
	|	Состав.Номенклатура,
	|	Состав.СерияНоменклатуры,
	|	Состав.КачествоНовое,
	|	Состав.КачествоСтарое,
	|	Состав.ЕдиницаИзмерения,
	|	Состав.Количество,
	|	""04"",
	|	ВЫБОР
	|		КОГДА Состав.КачествоНовое = ЗНАЧЕНИЕ(Справочник.Качество.Брак)
	|			ТОГДА ""344""
	|		ИНАЧЕ ""343""
	|	КОНЕЦ
	|ИЗ
	|	Документ.КорректировкаКачестваЗапасов.Товары КАК Состав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаКачестваЗапасов КАК Док
	|		ПО Состав.Ссылка = Док.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналОпераций
	|		ПО (Док.Ссылка = ЖурналОпераций.Объект)
	|			И (ЖурналОпераций.Источник = ""1С"")
	|			И (ЖурналОпераций.Приемник = &Партнер)
	|ГДЕ
	|	Состав.Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)
	|	И Док.Проведен
	|	И (ЖурналОпераций.Объект ЕСТЬ NULL
	|			ИЛИ ЖурналОпераций.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыЗаявок.Новая))
	|
	|УПОРЯДОЧИТЬ ПО
	|	влЗапрос.Дата
	|ИТОГИ
	|	МАКСИМУМ(ТипОперации),
	|	МАКСИМУМ(ТипДвижения)
	|ПО
	|	Ссылка";
	
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", СвойстваПартнера.ГруппаНоменклатуры); 
	Запрос.УстановитьПараметр("ОсновнойСклад", СвойстваПартнера.Склад); 
	Запрос.УстановитьПараметр("Партнер", Строка(Партнер)); 
	Запрос.УстановитьПараметр("КачествоПретензия", СвойстваПартнера.КачествоПретензия);
	
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

#КонецОбласти 

#Область ПОДТВЕРЖДЕНИЯ

Функция ЕстьЗаявкаНаОперациюОбновленияПоставок(НомерТранспортировки, СтатусТранспортировки)
	
	СтруктураОтбора = Новый Структура("НомерТранспортировки, Идентификатор", НомерТранспортировки, СтатусТранспортировки);

	Возврат ЗначениеЗаполнено(ТаблицаНовыхЗаявокНаОбновлениеСтатусовПоставки.НайтиСтроки(СтруктураОтбора));
	
КонецФункции

Процедура ДобавитьНовуюЗаявкуВТаблицуЗаявокНаОбновлениеПоставок(Объект, Идентификатор, НомерТранспортировки)
	
	НовСтр = ТаблицаНовыхЗаявокНаОбновлениеСтатусовПоставки.Добавить();
	НовСтр.Объект               = Объект;
	НовСтр.Идентификатор        = Идентификатор;
	НовСтр.НомерТранспортировки = НомерТранспортировки;
	
КонецПроцедуры
 
Функция ПолучитьЗаявкиНаОбновлениеПоставок()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Объект,
	|	ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Текст КАК СТРОКА(25)) КАК НомерТранспортировки,
	|	ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор КАК СТРОКА(36)) КАК Идентификатор
	|ИЗ
	|	РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций
	|ГДЕ
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Источник = ""1С""
	|	И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Приемник = &Приемник
	|	И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор В (""shipmentupdate_004"", ""shipmentupdate_005"")
	|	И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Статус = &Статус
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Объект,
	|	ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Текст КАК СТРОКА(25)),
	|	ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор КАК СТРОКА(36))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	влЗапрос.НомерТранспортировки,
	|	СвойстваОбъектов.Объект КАК Объект,
	|	СтатусыНакладных.Состояние,
	|	""004"" КАК СтатусТранспортировки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Объект КАК Объект,
	|		ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Текст КАК СТРОКА(25)) КАК НомерТранспортировки,
	|		ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор КАК СТРОКА(36)) КАК Идентификатор
	|	ИЗ
	|		РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций
	|	ГДЕ
	|		ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Источник = ""1С""
	|		И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Приемник = &Приемник
	|		И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор = ""shipmentupdate_004""
	|		И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Статус = &Статус
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Объект,
	|		ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Текст КАК СТРОКА(25)),
	|		ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор КАК СТРОКА(36))) КАК влЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваОбъектов
	|		ПО влЗапрос.НомерТранспортировки = СвойстваОбъектов.Значение
	|			И (СвойстваОбъектов.Свойство = &НомерПоДаннымПоставщика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеДокументовОтгрузки.СрезПоследних КАК СтатусыНакладных
	|		ПО (СвойстваОбъектов.Объект = СтатусыНакладных.ДокументОтгрузки)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СвойстваОбъектов.Объект) = ТИП(Документ.ОтгрузкаТоваровУслуг)
	|	И СтатусыНакладных.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияДокументовОтгрузки.Создан), ЗНАЧЕНИЕ(Перечисление.СостоянияДокументовОтгрузки.ВОтборе))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	влЗапрос.НомерТранспортировки,
	|	СвойстваОбъектов.Объект,
	|	СтатусыНакладных.Состояние,
	|	""005""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Объект КАК Объект,
	|		ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Текст КАК СТРОКА(25)) КАК НомерТранспортировки,
	|		ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор КАК СТРОКА(36)) КАК Идентификатор
	|	ИЗ
	|		РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций КАК ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций
	|	ГДЕ
	|		ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Источник = ""1С""
	|		И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Приемник = &Приемник
	|		И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор = ""shipmentupdate_005""
	|		И ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Статус = &Статус
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Объект,
	|		ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Текст КАК СТРОКА(25)),
	|		ВЫРАЗИТЬ(ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.Идентификатор КАК СТРОКА(36))) КАК влЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваОбъектов
	|		ПО влЗапрос.НомерТранспортировки = СвойстваОбъектов.Значение
	|			И (СвойстваОбъектов.Свойство = &НомерПоДаннымПоставщика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеДокументовОтгрузки.СрезПоследних КАК СтатусыНакладных
	|		ПО (СвойстваОбъектов.Объект = СтатусыНакладных.ДокументОтгрузки)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйСкладскойОрдер КАК РСО
	|		ПО (СвойстваОбъектов.Объект = РСО.ДокументОснование)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(СвойстваОбъектов.Объект) = ТИП(Документ.ОтгрузкаТоваровУслуг)
	|	И РСО.Ссылка ЕСТЬ NULL
	|	И СтатусыНакладных.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияДокументовОтгрузки.НеВыполнен)";
	
	Запрос.УстановитьПараметр("Приемник", Строка(Партнер));
	Запрос.УстановитьПараметр("Статус", Справочники.СтатусыЗаявок.Новая);
	Запрос.УстановитьПараметр("НомерПоДаннымПоставщика", СвойстваПартнера.Свойство_НомерТранспортировки); 
	
	Результат = Запрос.ВыполнитьПакет();
	
	РазблокированныеТранспортировки = Результат.Получить(0).Выгрузить();
	
	ЗаблокированныеТранспортировки = Результат.Получить(1).Выгрузить();
	
	Если ЗначениеЗаполнено(ЗаблокированныеТранспортировки) Тогда
		
		ТекстПричины004 = "";
		ОжидаемыйСтатусТранспортировкиДо004 = "Отобран";
		ОжидаемыйСтатусТранспортировкиДо005 = "Отгружен, Выполнен, Не выполнен";
		
		Для каждого СтрТаб Из ЗаблокированныеТранспортировки Цикл
			
			Сообщить(ОбщиеФункции._СтрШаблон_("Не удалось обновить статус транспортировки №%1 до статуса %2 по причине: '%3' имеет состояние '%4'. Ожидаемый статус должен быть '%5'",
				СтрТаб.НомерТранспортировки, СтрТаб.СтатусТранспортировки, СтрТаб.Объект, СтрТаб.Состояние, ?(СтрТаб.СтатусТранспортировки = "004", ОжидаемыйСтатусТранспортировкиДо004, ОжидаемыйСтатусТранспортировкиДо005)),
			СтатусСообщения.Внимание);
			
			СтрокаРазблокированныеТранспортировки = РазблокированныеТранспортировки.НайтиСтроки(Новый Структура("НомерТранспортировки, Идентификатор", СтрТаб.НомерТранспортировки, "shipmentupdate_" + СтрТаб.СтатусТранспортировки));
			
			Для каждого Элемент Из СтрокаРазблокированныеТранспортировки Цикл
				РазблокированныеТранспортировки.Удалить(Элемент);
			КонецЦикла; 
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат РазблокированныеТранспортировки;
	
КонецФункции

Процедура СоздатьЗаявкуНаОперациюОбновленияПоставок(Объект, МассивТранспортировок)
	
	РегистраторЗаписи004 = Документы.РегистраторЗаписи.СоздатьНовыйДокумент("" + Объект);
	
	РегистраторЗаписи005 = Документы.РегистраторЗаписи.СоздатьНовыйДокумент("" + Объект);
	
	НомерТранспортировки = ОбщиеФункции.ВернутьЗначениеСвойстваОбъекта(Объект, СвойстваПартнера.Свойство_НомерТранспортировки);
	
	Если МассивТранспортировок.Найти(НомерТранспортировки) = Неопределено Тогда
		
		Если НЕ ЕстьЗаявкаНаОперациюОбновленияПоставок(НомерТранспортировки, "shipmentupdate_004") Тогда
			
			_3PLСервер.ДобавитьЗаписьПоДокументуВЖурналОпераций(РегистраторЗаписи004, "1С", Строка(Партнер), 
				Справочники.СтатусыЗаявок.Новая, "shipmentupdate_004", ТекущаяДатаСеанса(),, НомерТранспортировки);
			
			ДобавитьНовуюЗаявкуВТаблицуЗаявокНаОбновлениеПоставок(РегистраторЗаписи004, "shipmentupdate_004", НомерТранспортировки);
			
		КонецЕсли;  
		
		Если НЕ ЕстьЗаявкаНаОперациюОбновленияПоставок(НомерТранспортировки, "shipmentupdate_005") Тогда
			
			_3PLСервер.ДобавитьЗаписьПоДокументуВЖурналОпераций(РегистраторЗаписи005, "1С", Строка(Партнер), 
				Справочники.СтатусыЗаявок.Новая, "shipmentupdate_005", ТекущаяДатаСеанса(),, НомерТранспортировки);
			
			ДобавитьНовуюЗаявкуВТаблицуЗаявокНаОбновлениеПоставок(РегистраторЗаписи005, "shipmentupdate_005", НомерТранспортировки);
			
		КонецЕсли;
			
		МассивТранспортировок.Добавить(НомерТранспортировки);
		
	КонецЕсли; 
		
КонецПроцедуры
 
Функция СформироватьТелоЗапросаДляОбновленияПоставки(Объект, НомерТранспортировки, ИдентификаторОперации)
	
	ЧасовойПояс = "RUS03";
	СмещениеВСекундах = 10; // для разницы в 10 сек
	СмещениеВМинутах = 600; // для разницы в 10 мин
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	
	Запись.ЗаписатьНачалоОбъекта();
	
		ЗаписатьСвойствоИЗначениеJSON(Запись, "interfaceName", "shipmentupdate");
		ЗаписатьСвойствоИЗначениеJSON(Запись, "plant", СвойстваПартнера.КодДистрибьютора);
		ЗаписатьСвойствоИЗначениеJSON(Запись, "shpnum", НомерТранспортировки);
		
		ЗаписатьСвойствоИЗначениеJSON(Запись, "event");
		Запись.ЗаписатьНачалоМассива();
		
		Если ИдентификаторОперации = "shipmentupdate_004" Тогда
			Запись.ЗаписатьНачалоОбъекта();
			ЗаписатьСвойствоИЗначениеJSON(Запись, "activityCode", 		"001");
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endDate", 			Формат(Объект.Дата, "ДФ=yyyyMMdd"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endTime", 			Формат(Объект.Дата, "ДФ=HHmmss"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endTimeZone", 		ЧасовойПояс);
			Запись.ЗаписатьКонецОбъекта();
			
			Запись.ЗаписатьНачалоОбъекта();
			ЗаписатьСвойствоИЗначениеJSON(Запись, "activityCode", 		"002");
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endDate", 			Формат(Объект.Дата, "ДФ=yyyyMMdd"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endTime", 			Формат(Объект.Дата + СмещениеВСекундах, "ДФ=HHmmss"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endTimeZone", 		ЧасовойПояс);
			Запись.ЗаписатьКонецОбъекта();
			
			Запись.ЗаписатьНачалоОбъекта();
			ЗаписатьСвойствоИЗначениеJSON(Запись, "activityCode", 		"003");
			ЗаписатьСвойствоИЗначениеJSON(Запись, "startDate", 			Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMdd"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "startTime", 			Формат(ТекущаяДатаСеанса(), "ДФ=HHmmss"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "startTimeZone", 		ЧасовойПояс);
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endDate", 			Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMdd"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endTime", 			Формат(ТекущаяДатаСеанса() + СмещениеВМинутах, "ДФ=HHmmss"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endTimeZone", 		ЧасовойПояс);
			Запись.ЗаписатьКонецОбъекта();
			
			Запись.ЗаписатьНачалоОбъекта();
			ЗаписатьСвойствоИЗначениеJSON(Запись, "activityCode", 		"004");
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endDate", 			Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMdd"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endTime", 			Формат(ТекущаяДатаСеанса() + (2 * СмещениеВМинутах), "ДФ=HHmmss"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "endTimeZone", 		ЧасовойПояс);
			Запись.ЗаписатьКонецОбъекта();
			
		Иначе
			
			Запись.ЗаписатьНачалоОбъекта();
			ЗаписатьСвойствоИЗначениеJSON(Запись, "activityCode", 		"005");
			ЗаписатьСвойствоИЗначениеJSON(Запись, "startDate", 			Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMdd"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "startTime", 			Формат(НачалоДня(ТекущаяДатаСеанса()) + 28800, "ДФ=HHmmss"));
			ЗаписатьСвойствоИЗначениеJSON(Запись, "startTimeZone", 		ЧасовойПояс);
			//ЗаписатьСвойствоИЗначениеJSON(Запись, "endDate", 			Формат(ТекущаяДатаСеанса(), "ДФ=yyyyMMdd"));
			//ЗаписатьСвойствоИЗначениеJSON(Запись, "endTime", 			Формат(НачалоДня(ТекущаяДатаСеанса()) + 82800, "ДФ=HHmmss"));
			//ЗаписатьСвойствоИЗначениеJSON(Запись, "endTimeZone", 		ЧасовойПояс);
			Запись.ЗаписатьКонецОбъекта();
			
		КонецЕсли;
	
		Запись.ЗаписатьКонецМассива();
	
	Запись.ЗаписатьКонецОбъекта();
	
	Возврат Запись.Закрыть();
	
КонецФункции
 
Функция СформироватьТелоЗапросаДляПодтвержденияПоставки(ДокументСсылка, ДокументПодтверждения, ТипОперации)
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ТипПоставки = "7";
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтгрузкаТоваровУслуг") Тогда 
		ТипПоставки = "J";
	Иначе
		ТипПоставки = "T";
	КонецЕсли;
	
	Если ТипПоставки = "J" Тогда
		ТипДействия = "PIC";
	Иначе
		ТипДействия = "POS";
	КонецЕсли;
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	
	Запись.ЗаписатьНачалоОбъекта();
	
	ЗаписатьСвойствоИЗначениеJSON(Запись, "plant",         СвойстваПартнера.КодДистрибьютора);
	ЗаписатьСвойствоИЗначениеJSON(Запись, "ordnum",        ОбщиеФункции.ВернутьЗначениеСвойстваОбъекта(ДокументСсылка, СвойстваПартнера.Свойство_НомерВходящегоДокумента));
	ЗаписатьСвойствоИЗначениеJSON(Запись, "action",        ТипДействия);
	ЗаписатьСвойствоИЗначениеJSON(Запись, "orddtpick",     Формат(ДокументПодтверждения.Дата, "ДФ=yyyyMMdd"));
	ЗаписатьСвойствоИЗначениеJSON(Запись, "ordtyptxt",     ТипПоставки);
	ЗаписатьСвойствоИЗначениеJSON(Запись, "interfaceName", "pickingconfirmation");
	
	Запись.ЗаписатьИмяСвойства("shipOrderDetails");
	
	Запись.ЗаписатьНачалоМассива();
	
	Если ТипПоставки <> "J" Тогда
	
		ДокументПодтвержденияТовары = ДокументПодтверждения.Товары;
		
		Для каждого СтрТабЧасть Из ДокументСсылка.Товары Цикл			
		
			Запись.ЗаписатьНачалоОбъекта();
			
			ЗаписатьСвойствоИЗначениеJSON(Запись, "linenum",    СтрТабЧасть.ИдСтрокиПоклажедателя);
			ЗаписатьСвойствоИЗначениеJSON(Запись, "itemnum",    СтрТабЧасть.Номенклатура.Артикул);
			
				Запись.ЗаписатьИмяСвойства("shipOrderStock");
				Запись.ЗаписатьНачалоОбъекта();
					ЗаписатьСвойствоИЗначениеJSON(Запись, "ordqty",     Формат(СтрТабЧасть.Количество, "ЧРД=.; ЧН=0; ЧГ=0"));
					ЗаписатьСвойствоИЗначениеJSON(Запись, "orduom",     ОпределитьЕдиницуИзмеренияПепси(СтрТабЧасть.ЕдиницаИзмерения, СтрТабЧасть.Номенклатура));
					ЗаписатьСвойствоИЗначениеJSON(Запись, "lot",        ?(ЗначениеЗаполнено(СтрТабЧасть.СерияНоменклатуры), СтрТабЧасть.СерияНоменклатуры.НомерПартииПоставщика, СвойстваПартнера.НомерВиртуальнойПартии)); 
				Запись.ЗаписатьКонецОбъекта();
			
			Запись.ЗаписатьКонецОбъекта();
			
			СтрокиПодтверждения = ДокументПодтвержденияТовары.НайтиСтроки(Новый Структура("Номенклатура", СтрТабЧасть.Номенклатура));
			
			КоличествоФакт = 0;
			
			Для каждого СтрПодтверждение Из СтрокиПодтверждения Цикл
				КоличествоФакт = КоличествоФакт + (СтрПодтверждение.Количество * СтрПодтверждение.Коэффициент / СтрТабЧасть.Коэффициент);
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
		
		СделкаТовары = ДокументСсылка.Сделка.Товары;
		
		Для каждого СтрТабЧасть Из ДокументПодтверждения.СерииОтбора Цикл
			
			СтрТабОснование = СделкаТовары.Найти(СтрТабЧасть.Номенклатура, "Номенклатура");
			
			Если СтрТабОснование = Неопределено Тогда
				Сообщить(ОбщиеФункции._СтрШаблон_("В документе '%1' не удалось найти товар '%2', который есть в отборе '%3'. Строка будет пропущена",
					ДокументСсылка, СтрТабЧасть.Номенклатура, ДокументПодтверждения));
				Продолжить;
			КонецЕсли; 
		
			Запись.ЗаписатьНачалоОбъекта();
			
			ЗаписатьСвойствоИЗначениеJSON(Запись, "linenum",    СтрТабОснование.ИдСтрокиПоклажедателя);
			ЗаписатьСвойствоИЗначениеJSON(Запись, "itemnum",    СтрТабЧасть.Номенклатура.Артикул);
			
			Если СтрТабЧасть.КоличествоПлан <> СтрТабЧасть.КоличествоФакт Тогда
				СтрокаЛистОтбора = ДокументПодтверждения.ЛистОтбора.Найти(СтрТабЧасть.Номенклатура, "Номенклатура");
				КодПричиныНеполнойКомплектации = СвойстваПартнера.ОсновнойКодПричиныНеполнойКомплектации;
				Если СтрокаЛистОтбора <> Неопределено И ЗначениеЗаполнено(СтрокаЛистОтбора.ПричинаНедобора) Тогда
					КодПричиныНеполнойКомплектации = ОбщиеФункции.ВернутьЗначениеСвойстваОбъекта(СтрокаЛистОтбора.ПричинаНедобора,
					СвойстваПартнера.Свойство_КодПричиныНеполнойКомплектацииПепси);
				КонецЕсли;
				ЗаписатьСвойствоИЗначениеJSON(Запись, "udreason", КодПричиныНеполнойКомплектации);
			КонецЕсли;
			
				Запись.ЗаписатьИмяСвойства("shipOrderStock");
				Запись.ЗаписатьНачалоОбъекта();
					ЗаписатьСвойствоИЗначениеJSON(Запись, "ordqty",     Формат(СтрТабЧасть.КоличествоФакт, "ЧРД=.; ЧН=0; ЧГ=0"));
					ЗаписатьСвойствоИЗначениеJSON(Запись, "orduom",     ОпределитьЕдиницуИзмеренияПепси(СтрТабЧасть.Номенклатура.ЕдиницаХраненияОстатков, СтрТабЧасть.Номенклатура));
					ЗаписатьСвойствоИЗначениеJSON(Запись, "lot",        ?(ЗначениеЗаполнено(СтрТабЧасть.СерияНоменклатурыОтбора), СтрТабЧасть.СерияНоменклатурыОтбора.НомерПартииПоставщика, СвойстваПартнера.НомерВиртуальнойПартии)); 
				Запись.ЗаписатьКонецОбъекта();
			
			Запись.ЗаписатьКонецОбъекта();
		
		КонецЦикла;
			
	КонецЕсли;  
	
	Запись.ЗаписатьКонецМассива();
	
	Запись.ЗаписатьКонецОбъекта();

	Возврат Запись.Закрыть();
	
КонецФункции

#КонецОбласти

#Область ТРАНСПОРТИРОВКИ

Функция ОпределитьДокументыДляТранспортировки(ТаблицаДанных)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	т.ДокументСсылка,
	|	т.НомерТранспортировки,
	|	т.НомерВходящегоДокумента
	|ПОМЕСТИТЬ втДокументыДляТранспортировки
	|ИЗ
	|	&ТаблицаДанных КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	влЗапрос.ДокументСсылка,
	|	влЗапрос.НомерТранспортировки,
	|	влЗапрос.НомерВходящегоДокумента,
	|	СоставМаршрута.Ссылка КАК МаршрутЭкспедитора
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(СвойстваДокументов.Объект, НЕОПРЕДЕЛЕНО) КАК ДокументСсылка,
	|		ДокументыДляТранспортировки.НомерТранспортировки КАК НомерТранспортировки,
	|		ДокументыДляТранспортировки.НомерВходящегоДокумента КАК НомерВходящегоДокумента
	|	ИЗ
	|		втДокументыДляТранспортировки КАК ДокументыДляТранспортировки
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваДокументов
	|			ПО ДокументыДляТранспортировки.НомерВходящегоДокумента = СвойстваДокументов.Значение
	|				И (СвойстваДокументов.Свойство = &Свойство_НомерВходящегоДокумента)) КАК влЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутЭкспедитора.ДокументыОснования КАК СоставМаршрута
	|		ПО влЗапрос.ДокументСсылка = СоставМаршрута.ДокументОснование";
	
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
	Запрос.УстановитьПараметр("Свойство_НомерВходящегоДокумента", СвойстваПартнера.Свойство_НомерВходящегоДокумента);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПреобразоватьДанныеПоТранспортировкамВТаблицуЗначений(Данные)
	
	ТабЗнач = Новый ТаблицаЗначений;
	ТабЗнач.Колонки.Добавить("ДокументСсылка",          Новый ОписаниеТипов("ДокументСсылка.ОтгрузкаТоваровУслуг"));
	ТабЗнач.Колонки.Добавить("НомерТранспортировки",    Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(36)));
	ТабЗнач.Колонки.Добавить("НомерВходящегоДокумента", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(36)));
	
	Для каждого Элемент Из Данные Цикл
		
		НомерТранспортировки = Элемент.internalLoadNum;
		
		СоставТранспортировки = Элемент.shipmentDetails;
		
		Для каждого СтрТранспортировки Из СоставТранспортировки Цикл
			
			НовСтр = ТабЗнач.Добавить();
			НовСтр.НомерТранспортировки = НомерТранспортировки;
			НовСтр.НомерВходящегоДокумента = УдалитьЛидирущиеНулиИзКодаТовара(СтрТранспортировки.shipmentId);
			
		КонецЦикла; 
		
	КонецЦикла; 
	
	Возврат ТабЗнач;
	
КонецФункции

#КонецОбласти 

#Область ДОКУМЕНТЫ

Процедура ОбновитьВыкупСХранения(ДляЗаполнения)
	
	
	
КонецПроцедуры
 
Процедура СоздатьИнвентаризациюДоставкиСводную(ДляЗаполнения)
	
	Если ДляЗаполнения.Свойство("ОшибкиВыполнения") = Ложь Тогда
		ВызватьИсключение "Не найдено обязательное свойство 'ОшибкиВыполнения'";
	ИначеЕсли ЗначениеЗаполнено(ДляЗаполнения.ОшибкиВыполнения) И ТипЗнч(ДляЗаполнения.ОшибкиВыполнения) <> Тип("Массив") Тогда
		ВызватьИсключение "Свойство 'ОшибкиВыполнения' должно быть массивом";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДляЗаполнения.ОшибкиВыполнения) Тогда
		ТекстСообщения = "";
		Для каждого Элемент Из ДляЗаполнения.ОшибкиВыполнения Цикл
			ТекстСообщения = ТекстСообщения + Элемент + Символы.ПС;
		КонецЦикла; 
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ТаблицаДокументовОтгрузки = НайтиДокументыТранспортировки(ДляЗаполнения.НомерТранспортировки);
	
	ДатаЭкспедиции = Дата(1,1,1);
	
	Если ЗначениеЗаполнено(ТаблицаДокументовОтгрузки) Тогда
		ДатаЭкспедиции = ТаблицаДокументовОтгрузки.Получить(0).ДатаЭкспедиции;
	Иначе
		ВызватьИсключение ОбщиеФункции._СтрШаблон_("Не удалось найти документы отгрузки по транспортировке №%1", ДляЗаполнения.НомерТранспортировки);
	КонецЕсли; 
	
	СведенияОВычерках = ПолучитьСведенияОВычерках(ТаблицаДокументовОтгрузки.ВыгрузитьКолонку("ДокументОтгрузки"));
	
	ТаблицаВычерков = СведенияОВычерках.ТаблицаВычерков;
	
	ДокументОбъект = Документы.ИнвентаризацияДоставкиСводная.СоздатьДокумент();
	ЗаполнитьЗначенияСвойств(ДокументОбъект, ДляЗаполнения);
	
	ДокументОбъект.ДатаЭкспедиции = ДатаЭкспедиции;
	ДокументОбъект.Автор = ПараметрыСеанса.ТекущийПользователь;
	ДокументОбъект.Комментарий = ОбщиеФункции._СтрШаблон_("Инвентаризация доставки по транпортировке №%1", ДляЗаполнения.НомерТранспортировки);
	
	СтруктураОтбора = Новый Структура("Номенклатура, СерияНоменклатуры, Качество");
	
	Для каждого СтрТаб Из ДляЗаполнения.Товары Цикл
		
		НовСтр = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, СтрТаб);
		
		НовСтр.Отгружено = СтрТаб.Количество;
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрТаб);
		
		ВычеркиПоНоменклатуре = ТаблицаВычерков.НайтиСтроки(СтруктураОтбора);
		
		НовСтр.Количество = 0;
		
		Для каждого ЭлМассива Из ВычеркиПоНоменклатуре Цикл
			Если СтрТаб.ЕдиницаИзмерения <> ЭлМассива.ЕдиницаИзмерения Тогда
				
				КоэффициентВходящий = СтрТаб.ЕдиницаИзмерения.Коэффициент;
				КоэффициентТекущий = ЭлМассива.ЕдиницаИзмерения.Коэффициент;
				
				Если КоэффициентВходящий > КоэффициентТекущий Тогда
					ЭлМассива.Количество = ЭлМассива.Количество / КоэффициентВходящий;
				ИначеЕсли КоэффициентВходящий < КоэффициентТекущий Тогда 
					ЭлМассива.Количество = ЭлМассива.Количество * КоэффициентТекущий;
				КонецЕсли; 
				
			КонецЕсли; 
			
			НовСтр.Количество = НовСтр.Количество + ЭлМассива.Количество;
			
		КонецЦикла;
		
		НовСтр.Передано = НовСтр.Отгружено - НовСтр.Количество;
		
	КонецЦикла;
	
	ДокументОбъект.ДокументыОтгрузки.Загрузить(ТаблицаДокументовОтгрузки);
	
	ДокументОбъект.Дата = ТекущаяДатаСеанса();
	
	ДокументОбъект.УстановитьНовыйНомер();
	
	ДокументОбъект.Записать();
	
	ОбщиеФункции.УстановитьЗначениеСвойства(Новый Структура("Объект, Свойство, Значение", ДокументОбъект.Ссылка, СвойстваПартнера.Свойство_НомерТранспортировки, ДляЗаполнения.НомерТранспортировки));
	
	ОбщиеФункции.УстановитьЗначениеСвойства(Новый Структура("Объект, Свойство, Значение", ДокументОбъект.Ссылка, СвойстваПартнера.Свойство_НомерВходящегоДокумента, ДляЗаполнения.НомерВходящегоДокумента));
	
	СтатусЗаявки = Справочники.СтатусыЗаявок.Принята;
	
	ОшибкиВыполнения = "";
	
	Если ЗначениеЗаполнено(СведенияОВычерках.ТаблицаДокументовБезИнвентаризацииДоставки) Тогда
		СтатусЗаявки = Справочники.СтатусыЗаявок.Черновик;
		ОшибкиВыполнения = "Документ отгрузки без инвентаризации доставки:
		|";
		Для каждого СтрТаб Из СведенияОВычерках.ТаблицаДокументовБезИнвентаризацииДоставки Цикл
			ОшибкиВыполнения = ОшибкиВыполнения + СтрТаб.ДокументОтгрузки + Символы.ПС;
		КонецЦикла; 
	КонецЕсли; 
	
	СтруктураЗаписи = РегистрыСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.ПолучитьСтруктуруЗаписи();
	СтруктураЗаписи.Объект                           = ДокументОбъект.Ссылка;
	СтруктураЗаписи.Источник                         = Строка(Партнер);
	СтруктураЗаписи.Приемник                         = "1С";
	СтруктураЗаписи.Статус                           = СтатусЗаявки;
	СтруктураЗаписи.Идентификатор                    = "#ПодтверждениеДоставки";
	СтруктураЗаписи.ДатаЗаписи                       = ТекущаяДатаСеанса();
	СтруктураЗаписи.ДатаВремяПолученияЗаявкиСервисом = ТекущаяДатаСеанса();
	СтруктураЗаписи.ОшибкиВыполнения                 = ОшибкиВыполнения;
	
	РегистрыСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.ДобавитьЗапись(СтруктураЗаписи);
	
КонецПроцедуры

Функция ПолучитьСведенияОВычерках(МассивДокументов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОтгрузкаТоваровУслуг.Ссылка КАК ДокументОтгрузки,
	|	ИнвДост.Ссылка КАК ИнвентаризацияДоставки
	|ПОМЕСТИТЬ втДокументыОтгрузки
	|ИЗ
	|	Документ.ОтгрузкаТоваровУслуг КАК ОтгрузкаТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияДоставки КАК ИнвДост
	|		ПО ОтгрузкаТоваровУслуг.Ссылка = ИнвДост.ДокументОтгрузки
	|ГДЕ
	|	ОтгрузкаТоваровУслуг.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДО.ДокументОтгрузки
	|ИЗ
	|	втДокументыОтгрузки КАК ДО
	|ГДЕ
	|	ДО.ИнвентаризацияДоставки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вычерки.Номенклатура,
	|	Вычерки.СерияНоменклатуры,
	|	Вычерки.Качество,
	|	Вычерки.ЕдиницаИзмерения,
	|	Вычерки.Коэффициент,
	|	Вычерки.Количество - Вычерки.Передано КАК Количество
	|ИЗ
	|	Документ.ИнвентаризацияДоставки.Товары КАК Вычерки
	|ГДЕ
	|	Вычерки.Ссылка В
	|			(ВЫБРАТЬ
	|				ДО.ИнвентаризацияДоставки
	|			ИЗ
	|				втДокументыОтгрузки КАК ДО)
	|	И Вычерки.Количество <> Вычерки.Передано";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов); 
	
	Результат = Запрос.ВыполнитьПакет();
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ТаблицаДокументовБезИнвентаризацииДоставки", Результат.Получить(1).Выгрузить());
	СтруктураВозврата.Вставить("ТаблицаВычерков", Результат.Получить(2).Выгрузить());
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция НайтиДокументыТранспортировки(НомерТранспортировки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	влЗапрос.ДокументОтгрузки,
	|	МАКСИМУМ(СоставМаршрута.Ссылка) КАК Маршрут,
	|	МАКСИМУМ(СоставМаршрута.Ссылка.ДатаЭкспедиции) КАК ДатаЭкспедиции
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(ЗначенияСвойствОбъектов.Объект КАК Документ.ОтгрузкаТоваровУслуг) КАК ДокументОтгрузки
	|	ИЗ
	|		РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|	ГДЕ
	|		ЗначенияСвойствОбъектов.Свойство = &Свойство
	|		И ЗначенияСвойствОбъектов.Значение = &Значение) КАК влЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутЭкспедитора.ДокументыОснования КАК СоставМаршрута
	|		ПО влЗапрос.ДокументОтгрузки = СоставМаршрута.ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	влЗапрос.ДокументОтгрузки";
	
	Запрос.УстановитьПараметр("Свойство", СвойстваПартнера.Свойство_НомерТранспортировки);
	Запрос.УстановитьПараметр("Значение", НомерТранспортировки); 
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции
 
Функция ВыбратьСериюНоменклатуры(Владелец, ДатаПроизводства, НомерПартии)
		
	Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	
	Если НЕ ЗначениеЗаполнено(ДатаПроизводства) Тогда
		Возврат Серия; 
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|		СерииНоменклатуры.Ссылка
	|	ИЗ
	|		Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|	ГДЕ
	|		СерииНоменклатуры.Владелец = &Владелец
	|		И СерииНоменклатуры.ДатаПроизводства = &ДатаПроизводства
	|		И СерииНоменклатуры.НомерПартииПоставщика = &НомерПартии";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("ДатаПроизводства", ДатаПроизводства);
	Запрос.УстановитьПараметр("НомерПартии", НомерПартии);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Серия = Выборка.Ссылка;
	КонецЕсли; 
	
	Возврат Серия;
	
КонецФункции

Функция ОпределитьТребуемыеДействияПоРаботеСДокументами(ТаблицаДанных)
	
	ТаблицаДанных.Колонки.Добавить("ТребуемоеДействие", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ДокументСсылка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	т.НомерВходящегоДокумента
	|ПОМЕСТИТЬ втДокументы
	|ИЗ
	|	&ТаблицаДанных КАК т;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документы.НомерВходящегоДокумента,
	|	ВЫБОР
	|		КОГДА СвойстваДокументов.Объект ЕСТЬ NULL
	|			ТОГДА ""Добавление""
	|		ИНАЧЕ ""Обновление""
	|	КОНЕЦ КАК ТребуемоеДействие,
	|   СвойстваДокументов.Объект КАК ДокументСсылка 
	|ИЗ
	|	втДокументы КАК Документы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК СвойстваДокументов
	|	ПО Документы.НомерВходящегоДокумента = СвойстваДокументов.Значение
	|		И СвойстваДокументов.Свойство = &Свойство_НомерВходящегоДокумента";
	
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
	Запрос.УстановитьПараметр("Свойство_НомерВходящегоДокумента", СвойстваПартнера.Свойство_НомерВходящегоДокумента);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрТаб Из ТаблицаДанных Цикл
		
		СтрокаРезультатаЗапроса = РезультатЗапроса.Найти(СтрТаб.НомерВходящегоДокумента, "НомерВходящегоДокумента");
		
		Если СтрокаРезультатаЗапроса <> Неопределено Тогда
			СтрТаб.ТребуемоеДействие = СтрокаРезультатаЗапроса.ТребуемоеДействие;
			СтрТаб.ДокументСсылка    = СтрокаРезультатаЗапроса.ДокументСсылка;
		КонецЕсли; 
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ПреобразоватьДанныеПоДокументамВТаблицуЗначений(Данные)
	
	ТабЗнач = Новый ТаблицаЗначений;
	ТабЗнач.Колонки.Добавить("НомерВходящегоДокумента", 		Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	ТабЗнач.Колонки.Добавить("ДатаВходящегоДокумента", 			Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТабЗнач.Колонки.Добавить("ДатаПоступления", 				Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТабЗнач.Колонки.Добавить("ТипДокумента", 					Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(1)));
	ТабЗнач.Колонки.Добавить("ВидОперации", 					Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(10)));
	ТабЗнач.Колонки.Добавить("Организация", 					Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТабЗнач.Колонки.Добавить("Склад", 							Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТабЗнач.Колонки.Добавить("Контрагент", 						Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТабЗнач.Колонки.Добавить("СтруктурнаяЕдиницаКонтрагента", 	Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницыКонтрагентов"));
	ТабЗнач.Колонки.Добавить("ДоговорКонтрагента", 				Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	ТабЗнач.Колонки.Добавить("Подразделение", 					Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	ТабЗнач.Колонки.Добавить("ТипЦен", 							Новый ОписаниеТипов("СправочникСсылка.ТипыЦенНоменклатурыКонтрагентов"));
	ТабЗнач.Колонки.Добавить("Автор", 							Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ТабЗнач.Колонки.Добавить("Ответственный", 					Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	ТабЗнач.Колонки.Добавить("ВидПоставки", 					Новый ОписаниеТипов("СправочникСсылка.ВидыПоставки"));
	ТабЗнач.Колонки.Добавить("СрокКредитования", 				Новый ОписаниеТипов("СправочникСсылка.СрокиКредитования"));
	ТабЗнач.Колонки.Добавить("СпособРасчетов", 					Новый ОписаниеТипов("ПеречислениеСсылка.СпособыРасчетов"));
	ТабЗнач.Колонки.Добавить("ДатаПоставки", 					Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТабЗнач.Колонки.Добавить("ДатаОтгрузки",					Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТабЗнач.Колонки.Добавить("Товары", 							Новый ОписаниеТипов("Массив"));
	ТабЗнач.Колонки.Добавить("ВозвратнаяТара", 					Новый ОписаниеТипов("Массив"));
	ТабЗнач.Колонки.Добавить("ОшибкиВыполнения", 				Новый ОписаниеТипов("Массив"));
	ТабЗнач.Колонки.Добавить("НаименованиеДоговораОтветХранения", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(150)));
	ТабЗнач.Колонки.Добавить("НомерТранспортировки", 			Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(15)));
	ТабЗнач.Колонки.Добавить("НеВыгружатьВТранзит", 			Новый ОписаниеТипов("Булево"));
	ТабЗнач.Колонки.Добавить("ГруппаДоставки", 					Новый ОписаниеТипов("СправочникСсылка.ГруппыДоставкиЗаказовПокупателей"));
	
	Для каждого Элемент Из Данные Цикл
	
		НовСтр = ТабЗнач.Добавить();
		НовСтр.НомерВходящегоДокумента         = Элемент.ordNum;
		НовСтр.ДатаВходящегоДокумента          = ПрочитатьДату(Элемент.timestamp, ФорматДатыJSON.ISO);
		НовСтр.ДатаПоступления                 = ПрочитатьДату(Элемент.ordDtDelv, "yyyyMMdd");
		НовСтр.ТипДокумента                    = Элемент.ordTypTxt;
		НовСтр.ВидОперации                     = Элемент.ordTyp2Txt;
		НовСтр.Организация                     = СвойстваПартнера.ОсновнаяОрганизация;
		НовСтр.Склад                           = СвойстваПартнера.Склад;
		НовСтр.СтруктурнаяЕдиницаКонтрагента   = ОбщиеФункции.ВернутьОбъектПоЗначениюСвойства(СвойстваПартнера.Свойство_КодГрузополучателя, Элемент.cusId);
		НовСтр.Контрагент                      = ?(Элемент.ordTypTxt = "7", СвойстваПартнера.ОсновнойПоставщик, НовСтр.СтруктурнаяЕдиницаКонтрагента.Владелец);
		НовСтр.ДоговорКонтрагента              = ?(Элемент.ordTypTxt = "7", СвойстваПартнера.ОсновнойДоговорПриемаНаХранение, Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
		НовСтр.ТипЦен                          = СвойстваПартнера.ОсновнойТипЦенКонтрагента;
		НовСтр.Подразделение                   = СвойстваПартнера.ОсновноеПодразделение;
		НовСтр.Автор                           = ПараметрыСеанса.ТекущийПользователь;
		НовСтр.Ответственный                   = ПараметрыСеанса.ТекущийПользователь;
		НовСтр.ВидПоставки                     = ?(Элемент.shipSeq = "RD", СвойстваПартнера.ВидПоставки_Самовывоз, СвойстваПартнера.ОсновнойВидПоставки);
		НовСтр.СпособРасчетов                  = СвойстваПартнера.ОсновнойСпособРасчетов;
		НовСтр.СрокКредитования                = СвойстваПартнера.ОсновнойСрокКредитования;
		НовСтр.ДатаПоставки                    = ПрочитатьДату(Элемент.ordDtDelv, "yyyyMMdd");
		НовСтр.ДатаОтгрузки                    = НовСтр.ДатаПоставки;
		НовСтр.НаименованиеДоговораОтветХранения = СвойстваПартнера.НаименованиеДоговораОтветХранения;
		НовСтр.НомерТранспортировки            = Элемент.returnShip;
		НовСтр.НеВыгружатьВТранзит             = Ложь;
		
		Если НовСтр.ТипДокумента <> "7" Тогда
			НовСтр.ГруппаДоставки              = ОпределитьГруппуДоставки(НовСтр.СтруктурнаяЕдиницаКонтрагента);
		КонецЕсли; 
		
		Если ((НовСтр.ТипДокумента = "J") ИЛИ (НовСтр.ТипДокумента = "T" И НовСтр.ВидОперации <> "ZLOR")) 
			И НовСтр.СтруктурнаяЕдиницаКонтрагента.Пустая() Тогда
			НовСтр.ОшибкиВыполнения.Добавить(ОбщиеФункции._СтрШаблон_("Не удалось найти структурную единицу (%1 %2,%3) по коду: %4",
				Элемент.cusNam, Элемент.city, Элемент.street, Элемент.cusId));
		КонецЕсли; 
		
		СоставДокумента = Элемент.orderDetails;
		
		Для каждого СтрДок Из СоставДокумента Цикл
			
			Если НовСтр.ТипДокумента = "7" И НЕ ЗначениеЗаполнено(НовСтр.НомерТранспортировки) Тогда
				
				НовСтр.НомерТранспортировки = СтрДок.stoNum;
				
			КонецЕсли; 
			
			СтрТоварПоклажедателя = ТаблицаТоваровПоклажедателя.Найти(УдалитьЛидирущиеНулиИзКодаТовара(СтрДок.itemNum), "Артикул");
			
			Если СтрТоварПоклажедателя = Неопределено Тогда
				НовСтр.ОшибкиВыполнения.Добавить(ОбщиеФункции._СтрШаблон_("Не найден товар с кодом '%1'", УдалитьЛидирущиеНулиИзКодаТовара(СтрДок.itemNum)));
				Продолжить;
			КонецЕсли;
			
			СерияНоменклатуры = Справочники.СерииНоменклатуры.ПустаяСсылка();
			
			Если ЗначениеЗаполнено(СтрДок.lot) Тогда
			// жесткое резервирование по партии
				СерияНоменклатуры = ВыбратьСериюНоменклатуры(СтрТоварПоклажедателя.Ссылка, ПрочитатьДату(СтрДок.prodDate, "dd.MM.yyyy"), СтрДок.lot);
				
				Если СерияНоменклатуры.Пустая() Тогда
					
					Попытка
						_3PLСервер.СоздатьСериюНоменклатуры(Новый Структура("Владелец, ДатаПроизводства, СрокГодности, НомерПартииПоставщика, Комментарий, Используется",
							СтрТоварПоклажедателя.Ссылка, ПрочитатьДату(СтрДок.prodDate, "dd.MM.yyyy"), ПрочитатьДату(СтрДок.sled, "dd.MM.yyyy"), СтрДок.lot, ОбщиеФункции._СтрШаблон_("#%1. Создана обменом по договору 3PL", СвойстваПартнера.КодДистрибьютора), Истина), СерияНоменклатуры);
					Исключение
						 НовСтр.ОшибкиВыполнения.Добавить(ОбщиеФункции._СтрШаблон_("Не удалось создать серию по товару '%1' (Номер партии: %2) по причине:%3", 
						 	СтрТоварПоклажедателя.Ссылка, СтрДок.lot, ОписаниеОшибки()));
					КонецПопытки;
						
				КонецЕсли; 
				
			КонецЕсли;
			
			МинСрок = СтрЗаменить(СтрДок.fresMin, "%", "");
			
			Если ТаблицаТоваровПоклажедателя.Колонки.Найти(СтрДок.ordUom) = Неопределено Тогда
				ЕдиницаИзмерения = СтрТоварПоклажедателя.EA;
			Иначе
				ЕдиницаИзмерения = СтрТоварПоклажедателя[СтрДок.ordUom];
			КонецЕсли; 
			
			СтруктураСтрокиДокумента = Новый Структура;
			
			СтруктураСтрокиДокумента.Вставить("Номенклатура", СтрТоварПоклажедателя.Ссылка);
				
			СтруктураСтрокиДокумента.Вставить("СерияНоменклатуры", СерияНоменклатуры);
			
			СтруктураСтрокиДокумента.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
				
			СтруктураСтрокиДокумента.Вставить("Коэффициент", СтруктураСтрокиДокумента.ЕдиницаИзмерения.Коэффициент);
			
			СтруктураСтрокиДокумента.Вставить("Количество", СтрДок.ordQty);
			
			СтруктураСтрокиДокумента.Вставить("Качество", ?(СтрДок.stockTyp = "S", СвойстваПартнера.КачествоДляБлокировки, Справочники.Качество.Стандарт));
			
			СтруктураСтрокиДокумента.Вставить("ДоговорВладельца", СвойстваПартнера.ОсновнойДоговорПриемаНаХранение);
			
			СтруктураСтрокиДокумента.Вставить("МинСрокГодности", МинСрок);
			
			СтруктураСтрокиДокумента.Вставить("ИдСтрокиПоклажедателя", Формат(СтрДок.lineNum, "ЧГ=0"));
			
			НовСтр.Товары.Добавить(СтруктураСтрокиДокумента);
			
		КонецЦикла; 
		
	КонецЦикла; 
	
	Возврат ТабЗнач
	
КонецФункции

Процедура СформироватьТаблицаТоваровПоклажедателя()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	Номенклатура.Артикул,
	|	Номенклатура.ЕдиницаЗакупок КАК EA,
	|	Номенклатура.ЕдиницаЗакупок2 КАК CS,
	|	Номенклатура.ЕдиницаЗакупок3 КАК PF
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В ИЕРАРХИИ (&ГруппаНоменклатуры)
	|	И Номенклатура.Артикул <> """"
	|	И НЕ Номенклатура.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", СвойстваПартнера.ГруппаНоменклатуры);
	
	ТаблицаТоваровПоклажедателя = Запрос.Выполнить().Выгрузить();
	
	ТаблицаТоваровПоклажедателя.Индексы.Добавить("Артикул, Ссылка");
	
КонецПроцедуры

#КонецОбласти 

#Область Номенклатура

Процедура ОпределитьПериодГодности(ПериодГодности, КоличествоПериодов, ПериодГодностиНоменклатуры, КоличествоПериодовГодностиНоменклатуры) Экспорт
	
	// Единица измерения срока годности. Значения:
	//	'D ' - День; 
	//	'W' - Неделя; 
	//	'M' - Месяц; 
	//	'Y' – Год.
	// Если переданная единица – неделя или год, СУСК заменяет их на день и месяц соответственно, с соответствующим пересчетом величины shelf life.
	// Поле должно быть выведено в интерфейс для просмотра и редактирования.
	// Значение по умолчанию – день.
	
	Если ПериодГодности = "D" Тогда
		ПериодГодностиНоменклатуры = Перечисления.Периодичность.День;
	ИначеЕсли ПериодГодности = "M" Тогда
		ПериодГодностиНоменклатуры = Перечисления.Периодичность.Месяц;
	ИначеЕсли ПериодГодности = "W" Тогда
		ПериодГодностиНоменклатуры = Перечисления.Периодичность.День;
		КоличествоПериодов = КоличествоПериодов * 7;
	ИначеЕсли ПериодГодности = "Y" Тогда
		ПериодГодностиНоменклатуры = Перечисления.Периодичность.Месяц;
		КоличествоПериодов = КоличествоПериодов * 12;
	КонецЕсли;
	
	КоличествоПериодовГодностиНоменклатуры = КоличествоПериодов;
	
КонецПроцедуры

Процедура ОпределитьБазовуюЕдиницу(БазоваяЕдиница, БазоваяЕдиницаИзмеренияНоменклатуры)
	
	//Тип упаковки. Значения:
	//	PF - паллета, 
	//	CS - коробка, 
	//	EA – штука,
	//Или обозначение базовой единицы измерения товара, интерпретируемое как штука.
	
	Если БазоваяЕдиница = "EA"
		ИЛИ НЕ (БазоваяЕдиница = "CS" ИЛИ БазоваяЕдиница = "PF") Тогда
		БазоваяЕдиницаИзмеренияНоменклатуры = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("796");
	ИначеЕсли БазоваяЕдиница = "CS" Тогда
		БазоваяЕдиницаИзмеренияНоменклатуры = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("778");
	ИначеЕсли БазоваяЕдиница = "PF" Тогда
		БазоваяЕдиницаИзмеренияНоменклатуры = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("пал");
	КонецЕсли; 
	
КонецПроцедуры

Функция ПересчитатьГабаритыВМетры(Размер, ЕдиницаИзмерения) Экспорт
	
	РазмерСУчетомПересчета = Размер;
	
	Если ЕдиницаИзмерения = "DM" Тогда
		РазмерСУчетомПересчета = Размер / 10;
	ИначеЕсли ЕдиницаИзмерения = "CM" Тогда
		РазмерСУчетомПересчета = Размер / 100;
	ИначеЕсли ЕдиницаИзмерения = "MM" Тогда
		РазмерСУчетомПересчета = Размер / 1000;
	КонецЕсли; 
	
	Возврат РазмерСУчетомПересчета;
	
КонецФункции

Функция ПреобразоватьДанныеПоТоварамВТаблицуЗначений(Данные)
	
	ТабЗнач = Новый ТаблицаЗначений;
	ТабЗнач.Колонки.Добавить("Родитель",                   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТабЗнач.Колонки.Добавить("Артикул",                    Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(25)));
	ТабЗнач.Колонки.Добавить("Наименование",               Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(120)));
	ТабЗнач.Колонки.Добавить("НаименованиеПолное",         Новый ОписаниеТипов("Строка"));
	ТабЗнач.Колонки.Добавить("Весовой",                    Новый ОписаниеТипов("Булево"));
	ТабЗнач.Колонки.Добавить("УчетПартий_в_ВМС",           Новый ОписаниеТипов("Булево"));
	ТабЗнач.Колонки.Добавить("ВидНоменклатуры",            Новый ОписаниеТипов("СправочникСсылка.ВидыНоменклатуры"));
	ТабЗнач.Колонки.Добавить("ДистрибьюторскийКонтракт",   Новый ОписаниеТипов("СправочникСсылка.ДистрибьюторскиеКонтракты"));
	ТабЗнач.Колонки.Добавить("ОсновнойСкладОтгрузки",      Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТабЗнач.Колонки.Добавить("ГруппаМатериалов",           Новый ОписаниеТипов("СправочникСсылка.ГруппыМатериалов"));
	ТабЗнач.Колонки.Добавить("ГруппаХранения",             Новый ОписаниеТипов("СправочникСсылка.ГруппыХранения"));
	ТабЗнач.Колонки.Добавить("ГруппаПополнения",           Новый ОписаниеТипов("СправочникСсылка.ГруппыПополнения"));
	ТабЗнач.Колонки.Добавить("ГруппаПикинга",              Новый ОписаниеТипов("СправочникСсылка.ГруппыПикинга"));
	ТабЗнач.Колонки.Добавить("ФормФактор",                 Новый ОписаниеТипов("СправочникСсылка.ФормФакторы"));
	ТабЗнач.Колонки.Добавить("ОсновнойПоставщик",          Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТабЗнач.Колонки.Добавить("ПериодГодности",             Новый ОписаниеТипов("ПеречислениеСсылка.Периодичность"));
	ТабЗнач.Колонки.Добавить("КоличествоПериодовГодности", Новый ОписаниеТипов("Число",  , , Новый КвалификаторыЧисла(5)));
	ТабЗнач.Колонки.Добавить("БазоваяЕдиницаИзмерения",    Новый ОписаниеТипов("СправочникСсылка.КлассификаторЕдиницИзмерения"));
	ТабЗнач.Колонки.Добавить("СтавкаНДС",                  Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	ТабЗнач.Колонки.Добавить("ЕдиницаЗакупок",             Новый ОписаниеТипов("Структура"));
	ТабЗнач.Колонки.Добавить("ЕдиницаЗакупок2",            Новый ОписаниеТипов("Структура"));
	ТабЗнач.Колонки.Добавить("ЕдиницаЗакупок3",            Новый ОписаниеТипов("Структура"));
	ТабЗнач.Колонки.Добавить("МодельСкладскогоУчета",      Новый ОписаниеТипов("СправочникСсылка.МоделиСкладскогоУчета"));
	
	МодельСкладскогоУчета_СоСроком   = Справочники.МоделиСкладскогоУчета.НайтиПоКоду("О00002"); // Со сроком годности
	МодельСкладскогоУчета_БезСрока   = Справочники.МоделиСкладскогоУчета.НайтиПоКоду("О00001"); // Без сроков
	
	Для каждого Элемент Из Данные Цикл
		
		СведенияОТоваре = Элемент.matMasItems.Получить(0);
		
		НовСтр = ТабЗнач.Добавить();
		НовСтр.ДистрибьюторскийКонтракт = СвойстваПартнера.Контракт;
		НовСтр.ОсновнойСкладОтгрузки    = СвойстваПартнера.Склад;
		НовСтр.ОсновнойПоставщик        = СвойстваПартнера.ОсновнойПоставщик;
		НовСтр.Родитель                 = СвойстваПартнера.ГруппаНоменклатуры;
		НовСтр.ГруппаМатериалов         = СвойстваПартнера.ГруппаМатериалов;
		НовСтр.ГруппаХранения           = СвойстваПартнера.ГруппаХранения;
		НовСтр.ГруппаПополнения         = СвойстваПартнера.ГруппаПополнения;
		НовСтр.ГруппаПикинга            = СвойстваПартнера.ГруппаПикинга;
		НовСтр.ФормФактор               = СвойстваПартнера.ФормФактор;
		НовСтр.Артикул                  = УдалитьЛидирущиеНулиИзКодаТовара(СведенияОТоваре.itemNum);
		НовСтр.Наименование             = СведенияОТоваре.descr;
		НовСтр.НаименованиеПолное       = СведенияОТоваре.descr;
		НовСтр.УчетПартий_в_ВМС         = Ложь;
		НовСтр.ВидНоменклатуры          = Справочники.ВидыНоменклатуры.НайтиПоКоду("О00001");
		НовСтр.СтавкаНДС                = Перечисления.СтавкиНДС.НДС20;
		НовСтр.МодельСкладскогоУчета    = ?(СведенияОТоваре.lotCaptureReq = "X", МодельСкладскогоУчета_СоСроком, МодельСкладскогоУчета_БезСрока);
		
		ОпределитьПериодГодности(СведенияОТоваре.shelfLifeUom, СведенияОТоваре.shelfLife, НовСтр.ПериодГодности, НовСтр.КоличествоПериодовГодности);
		
		ОпределитьБазовуюЕдиницу(СведенияОТоваре.baseUom, НовСтр.БазоваяЕдиницаИзмерения);
		
		Для каждого Единица Из СведенияОТоваре.itemConfigUOMS Цикл
			
			Если (СведенияОТоваре.itemTypTxt = "ZFIN" И ЗначениеЗаполнено(Единица.barCode))
				ИЛИ (СведенияОТоваре.itemTypTxt = "ZEMT" И Единица.uomTypTxt = "EA" И НовСтр.ЕдиницаЗакупок.Количество() = 0) Тогда
				
				Если Единица.uomTypTxt = "EA" Тогда
					ИмяКолонки = "ЕдиницаЗакупок";
				ИначеЕсли Единица.uomTypTxt = "CS" Тогда
					ИмяКолонки = "ЕдиницаЗакупок2";
				ИначеЕсли Единица.uomTypTxt = "PF" Тогда
					ИмяКолонки = "ЕдиницаЗакупок3";
				Иначе
					Продолжить;
				КонецЕсли;  
				
				ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("796");
				
				ОпределитьБазовуюЕдиницу(Единица.uomTypTxt, ЕдиницаПоКлассификатору);
				
				// Перепроверим единицу по классификатору, возможно это блок
				Если НРЕГ(ЕдиницаПоКлассификатору.Наименование) = "шт"
					И Единица.convFactor > 1 Тогда
					ЕдиницаПоКлассификатору = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("704");
				КонецЕсли; 
				
				НовСтр[ИмяКолонки].Вставить("Наименование",            ЕдиницаПоКлассификатору.Наименование + ?(Единица.convFactor > 1, Строка(Единица.convFactor), ""));
				НовСтр[ИмяКолонки].Вставить("ЕдиницаПоКлассификатору", ЕдиницаПоКлассификатору);
				НовСтр[ИмяКолонки].Вставить("Вес",                     ?(Единица.wgtMu = "KG",Единица.wgt, Окр(Единица.wgt / 1000, 6)));
				НовСтр[ИмяКолонки].Вставить("ВесБрутто",               ?(Единица.wgtMu = "KG",Единица.wgt, Окр(Единица.wgt / 1000, 6)));
				НовСтр[ИмяКолонки].Вставить("Глубина",                 ПересчитатьГабаритыВМетры(Единица.lgth, Единица.lgthMu));
				НовСтр[ИмяКолонки].Вставить("Высота",                  ПересчитатьГабаритыВМетры(Единица.hgt,Единица.hgtMu));
				НовСтр[ИмяКолонки].Вставить("Ширина",                  ПересчитатьГабаритыВМетры(Единица.wid,Единица.widMu));
				НовСтр[ИмяКолонки].Вставить("КодовоеОбозначениеВКПК",  ЕдиницаПоКлассификатору.Наименование);
				НовСтр[ИмяКолонки].Вставить("Комплектация",            Истина);
				НовСтр[ИмяКолонки].Вставить("Коэффициент",             Единица.convFactor);
				НовСтр[ИмяКолонки].Вставить("Объем",                   Окр((НовСтр[ИмяКолонки].Глубина * НовСтр[ИмяКолонки].Ширина * НовСтр[ИмяКолонки].Высота) / 100, 6));
				НовСтр[ИмяКолонки].Вставить("ОбъемБрутто",             Окр((НовСтр[ИмяКолонки].Глубина * НовСтр[ИмяКолонки].Ширина * НовСтр[ИмяКолонки].Высота) / 100, 6));
				НовСтр[ИмяКолонки].Вставить("ТехнологическаяУпаковка", ?(ЕдиницаПоКлассификатору.Код = "778",
				Перечисления.ВидыТехнологическихУпаковок.PACK,         Перечисления.ВидыТехнологическихУпаковок.ST));
				НовСтр[ИмяКолонки].Вставить("Штрихкод",                Единица.barCode);
				
			КонецЕсли; 
			
		КонецЦикла; 
		
	КонецЦикла;
	
	Возврат ТабЗнач;
	
КонецФункции

Функция ОпределитьТребуемыеДействияПоРаботеСНоменклатурой(ТаблицаДанных)
	
	ТаблицаДанных.Колонки.Добавить("ТребуемоеДействие", Новый ОписаниеТипов("Строка"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	т.Наименование,
	|	т.Артикул,
	|	т.ДистрибьюторскийКонтракт
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&ТаблицаДанных КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Наименование,
	|	Товары.Артикул,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.Ссылка ЕСТЬ NULL
	|			ТОГДА ""Добавление""
	|		ИНАЧЕ ""Обновление""
	|	КОНЕЦ КАК ТребуемоеДействие
	|ИЗ
	|	втТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Номенклатура.Ссылка КАК Ссылка,
	|			Номенклатура.Артикул КАК Артикул
	|		ИЗ
	|			Справочник.Номенклатура КАК Номенклатура
	|		ГДЕ
	|			Номенклатура.Ссылка В ИЕРАРХИИ(&ГруппаНоменклатуры)) КАК СпрНоменклатура
	|		ПО Товары.Артикул = СпрНоменклатура.Артикул";
	
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", СвойстваПартнера.ГруппаНоменклатуры); 
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрТаб Из ТаблицаДанных Цикл
		
		СтрокаРезультатаЗапроса = РезультатЗапроса.Найти(СтрТаб.Артикул, "Артикул");
		
		Если СтрокаРезультатаЗапроса <> Неопределено Тогда
			СтрТаб.ТребуемоеДействие = СтрокаРезультатаЗапроса.ТребуемоеДействие;
		КонецЕсли; 
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#Область ПАРТИИ

Функция ОпределитьТребуемыеДействияПоРаботеСПартиями(ТаблицаДанных)
	
	ТаблицаДанных.Колонки.Добавить("ТребуемоеДействие", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("СерияНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	т.НомерПартииПоставщика,
	|	т.Владелец,
	|   т.ДатаПроизводства
	|ПОМЕСТИТЬ втПартии
	|ИЗ
	|	&ТаблицаДанных КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партии.Владелец,
	|	Партии.НомерПартииПоставщика,
	|	ВЫБОР
	|		КОГДА СпрСерии.Ссылка ЕСТЬ NULL
	|			ТОГДА ""Добавление""
	|		ИНАЧЕ ""Обновление""
	|	КОНЕЦ КАК ТребуемоеДействие,
	|   СпрСерии.Ссылка КАК СерияНоменклатуры
	|ИЗ
	|	втПартии КАК Партии
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СпрСерии
	|		ПО Партии.Владелец= СпрСерии.Владелец
	|			И Партии.НомерПартииПоставщика = СпрСерии.НомерПартииПоставщика
	|  			И НАЧАЛОПЕРИОДА(Партии.ДатаПроизводства, ДЕНЬ) = СпрСерии.ДатаПроизводства";
	
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрТаб Из ТаблицаДанных Цикл
		
		СтрокаРезультатаЗапроса = РезультатЗапроса.Найти(СтрТаб.НомерПартииПоставщика, "НомерПартииПоставщика");
		
		Если СтрокаРезультатаЗапроса <> Неопределено Тогда
			СтрТаб.ТребуемоеДействие = СтрокаРезультатаЗапроса.ТребуемоеДействие;
			СтрТаб.СерияНоменклатуры = СтрокаРезультатаЗапроса.СерияНоменклатуры;
		КонецЕсли; 
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ПреобразоватьДанныеПоПартиямВТаблицуЗначений(Данные)
	
	ТабЗнач = Новый ТаблицаЗначений;
	ТабЗнач.Колонки.Добавить("Владелец",                   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТабЗнач.Колонки.Добавить("КоличествоПериодовГодности", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(5)));
	ТабЗнач.Колонки.Добавить("Комментарий",                Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(500)));
	ТабЗнач.Колонки.Добавить("НомерПартииПоставщика",      Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(25)));
	ТабЗнач.Колонки.Добавить("ПериодГодности",             Новый ОписаниеТипов("ПеречислениеСсылка.Периодичность"));
	ТабЗнач.Колонки.Добавить("ДатаПроизводства",           Новый ОписаниеТипов("Дата"));
	ТабЗнач.Колонки.Добавить("СрокГодности",               Новый ОписаниеТипов("Дата"));
	ТабЗнач.Колонки.Добавить("Производитель",              Новый ОписаниеТипов("СправочникСсылка.Контрагенты,СправочникСсылка.Производители"));
	ТабЗнач.Колонки.Добавить("СтранаПроисхождения",        Новый ОписаниеТипов("СправочникСсылка.КлассификаторСтранМира"));
	ТабЗнач.Колонки.Добавить("Подразделение",              Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	ТабЗнач.Колонки.Добавить("ДатаСозданияСерии",          Новый ОписаниеТипов("Дата"));
	ТабЗнач.Колонки.Добавить("Используется",               Новый ОписаниеТипов("Булево"));
	ТабЗнач.Колонки.Добавить("ПометкаУдаления",            Новый ОписаниеТипов("Булево"));
	ТабЗнач.Колонки.Добавить("КодПартнера",                Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(25)));
	
	Для каждого Элемент Из Данные Цикл
		
		Владелец = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", УдалитьЛидирущиеНулиИзКодаТовара(Элемент.itemNum), СвойстваПартнера.ГруппаНоменклатуры);
		
		НовСтр                            = ТабЗнач.Добавить();
		НовСтр.Владелец                   = Владелец;
		НовСтр.Комментарий                = ОбщиеФункции._СтрШаблон_("#%1. Создана обменом по договору 3PL", СвойстваПартнера.КодДистрибьютора);
		НовСтр.НомерПартииПоставщика      = Элемент.lot;
		НовСтр.ДатаПроизводства           = ПрочитатьДату(Элемент.prodDate, ФорматДатыJSON.ISO);
		НовСтр.СрокГодности               = ПрочитатьДату(Элемент.expDate, ФорматДатыJSON.ISO);
		НовСтр.ПериодГодности             = Перечисления.Периодичность.День;
		НовСтр.КоличествоПериодовГодности = (НовСтр.СрокГодности - НовСтр.ДатаПроизводства) / 86400;

		Производитель                     = ОбщиеФункции.ВернутьОбъектПоЗначениюСвойства(ПланыВидовХарактеристик.СвойстваОбъектов.ПолучитьСвойствоПоИмени("ИдентификаторЗаводаПепси"),
												Элемент.valType);

		Если НЕ ЗначениеЗаполнено(Производитель) Тогда
			Производитель                 = СвойстваПартнера.ОсновнойПоставщик;
		КонецЕсли;

		НовСтр.Производитель              = Производитель;
		НовСтр.СтранаПроисхождения        = Справочники.КлассификаторСтранМира.НайтиПоКоду("643");
		НовСтр.ДатаСозданияСерии          = ТекущаяДатаСеанса();
		НовСтр.Используется               = ?(Элемент.status = "X" ИЛИ ЗначениеЗаполнено(Элемент.del), Ложь, Истина);
		НовСтр.ПометкаУдаления            = ?(ЗначениеЗаполнено(Элемент.del), Истина, Ложь);
		НовСтр.КодПартнера                = УдалитьЛидирущиеНулиИзКодаТовара(Элемент.itemNum);
				
	КонецЦикла;
	
	Возврат ТабЗнач;
	
КонецФункции

#КонецОбласти 

#КонецОбласти
