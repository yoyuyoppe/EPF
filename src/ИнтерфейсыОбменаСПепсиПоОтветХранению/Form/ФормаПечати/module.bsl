Перем мИмяСохраненныхНастроек;

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	СохранитьНастройки();
КонецПроцедуры

Процедура СохранитьНастройки()
	
	СтруктураНастроек = ПолучитьСтруктуруНастроек();
	СохранитьЗначение(мИмяСохраненныхНастроек, СтруктураНастроек);

КонецПроцедуры

Функция ПолучитьСтруктуруНастроек()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("НаЭкран"          , НаЭкран);
	СтруктураНастроек.Вставить("КолВоЭкземпляров" , КолВоЭкземпляров);
	СтруктураНастроек.Вставить("ИмяПринтера"      , ИмяПринтера);	
	//СтруктураНастроек.Вставить("ПовторнаяПечать"  , ПовторнаяПечать);
	СтруктураНастроек.Вставить("Сохранить"        , Сохранить);
	СтруктураНастроек.Вставить("ТолькоСохранить"  , ТолькоСохранить);
	
	Возврат СтруктураНастроек;   
КонецФункции

Процедура ВосстановитьНастройки()
	
	СтруктураНастроек = ПолучитьСтруктуруНастроек();
	
	Настройки = ВосстановитьЗначение(мИмяСохраненныхНастроек);	
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		Для Каждого ТекКлючЗнач ИЗ Настройки Цикл			
			Если СтруктураНастроек.Свойство(ТекКлючЗнач.Ключ) Тогда
				ЭтаФорма[ТекКлючЗнач.Ключ] = ТекКлючЗнач.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	КолВоЭкземпляров = "1";
	ИмяПринтера = ПараметрыСеанса.ИмяПринтераПоУмолчанию;
	Для Каждого ТекЗнач Из ПараметрыСеанса.СписокПринтеров Цикл 
		ЭлементыФормы.ИмяПринтера.СписокВыбора.Добавить(ТекЗнач);
	КонецЦикла;
	//ОчередьПечати = Справочники.ОчередиПечати.НайтиПоНаименованию("Объединенные кондитеры", Истина);
		
	ВосстановитьНастройки();
	ПереключательПриИзменении(Неопределено);
	ТолькоСохранитьПриИзменении(Неопределено);
КонецПроцедуры

Процедура ПереключательПриИзменении(Элемент)
	Если НаЭкран = 1 Тогда 
		ТекДоступность = Ложь;
	Иначе
		ТекДоступность = Истина;
	КонецЕсли;
	
	ЭлементыФормы.ИмяПринтера.Доступность          = ТекДоступность;
	ЭлементыФормы.ОчередьПечати.Доступность        = ТекДоступность;
	ЭлементыФормы.НадписьОчередьПечати.Доступность = ТекДоступность;
	ЭлементыФормы.ПовторнаяПечать.Доступность      = ТекДоступность;
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	ЕстьОшибки = Ложь;
	
	Если (Сохранить ИЛИ ТолькоСохранить) И НЕ ЗначениеЗаполнено(КаталогСохранения) Тогда
		Сообщить("Не заполнено значение ""Каталог сохранения"".");
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда 
		Возврат;
	КонецЕсли;
	
	ТекОчередьПечати = Неопределено;
	Если НаЭкран <> 1 И ЗначениеЗаполнено(ОчередьПечати) Тогда 
		ТекОчередьПечати = ОчередьПечати;
	КонецЕсли; 
	
	ПараметрыПечати = Новый Структура;	
	ПараметрыПечати.Вставить("НаПринтер"         , ?(НаЭкран = 1, Ложь, Истина));
	ПараметрыПечати.Вставить("КолВоЭкземпляров"  , КолВоЭкземпляров);
	ПараметрыПечати.Вставить("ИмяПринтера"       , ИмяПринтера);
	ПараметрыПечати.Вставить("ОчередьПечати"     , ТекОчередьПечати);
	ПараметрыПечати.Вставить("ПовторнаяПечать"   , ПовторнаяПечать);
	ПараметрыПечати.Вставить("Сохранить"         , Сохранить);
	ПараметрыПечати.Вставить("ТолькоСохранить"   , ТолькоСохранить);
	ПараметрыПечати.Вставить("КаталогСохранения" , КаталогСохранения);
	
	ЭтаФорма.Закрыть(ПараметрыПечати);
	
КонецПроцедуры

Процедура ТолькоСохранитьПриИзменении(Элемент)
	ТекДоступность = НЕ ТолькоСохранить;
	
	ЭлементыФормы.ИмяПринтера.Доступность          = ТекДоступность;
	ЭлементыФормы.ОчередьПечати.Доступность        = ТекДоступность;
	ЭлементыФормы.НадписьОчередьПечати.Доступность = ТекДоступность;
	ЭлементыФормы.ПовторнаяПечать.Доступность      = ТекДоступность;
	ЭлементыФормы.КолВоЭкземпляров.Доступность     = ТекДоступность;
	ЭлементыФормы.НаЭкран.Доступность              = ТекДоступность;
	ЭлементыФормы.НаПринтер.Доступность            = ТекДоступность;
	ЭлементыФормы.Сохранить.Доступность            = ТекДоступность;
	ЭлементыФормы.КаталогСохранения.Доступность    = ТекДоступность;
	
	СохранитьПриИзменении(Неопределено);
КонецПроцедуры

Процедура ОсновныеДействияФормыОтмена(Кнопка)
	ЭтаФорма.Закрыть();
КонецПроцедуры

Процедура КаталогСохраненияНачалоВыбора(Элемент, СтандартнаяОбработка)
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбора.Заголовок = "Укажите каталог для сохранения";
	Если ДиалогВыбора.Выбрать() Тогда
		ЭтаФорма.КаталогСохранения = ДиалогВыбора.Каталог;
	КонецЕсли;
КонецПроцедуры

Процедура СохранитьПриИзменении(Элемент)
	Если Сохранить Или ТолькоСохранить Тогда 
		ТекДоступность = Истина;
	Иначе 
		ТекДоступность = Ложь;
	КонецЕсли;
	ЭлементыФормы.КаталогСохранения.Доступность = ТекДоступность;
КонецПроцедуры



мИмяСохраненныхНастроек = "ПечатьПакетаДокументов_ОК";